const std = @import("std");
const abi = @import("abi_types.zig");
const geometry = @import("geometry.zig");
const interaction = @import("interaction.zig");
const layout_pass = @import("layout_pass.zig");
const state_validation = @import("state_validation.zig");
const navigation = @import("navigation.zig");
const mutation = @import("mutation.zig");
const viewport = @import("viewport.zig");
const workspace = @import("workspace.zig");
const ID_SLOT_COUNT: usize = abi.MAX_WINDOWS * 2;
const EMPTY_SLOT: i64 = -1;
pub const RUNTIME_ANIMATION_NONE: u8 = 0;
pub const RUNTIME_ANIMATION_MUTATION: u8 = 1;
pub const RUNTIME_ANIMATION_WORKSPACE_SWITCH: u8 = 2;
pub const NIRI_MUTATION_ANIMATION_DURATION: f64 = 0.18;
pub const NIRI_WORKSPACE_SWITCH_ANIMATION_DURATION: f64 = 0.20;
const NIRI_MUTATION_APPEAR_OFFSET: f64 = 24.0;
const NIRI_WORKSPACE_SWITCH_OFFSET: f64 = 32.0;
const NIRI_VIEWPORT_SPRING_RESPONSE: f64 = 0.22;
const NIRI_VIEWPORT_SPRING_DAMPING: f64 = 0.95;
const NIRI_VIEWPORT_SPRING_EPSILON: f64 = 0.5;
const NIRI_VIEWPORT_SPRING_VELOCITY_EPSILON: f64 = 8.0;
const NIRI_VIEWPORT_REDUCED_RESPONSE: f64 = 0.18;
const NIRI_VIEWPORT_REDUCED_DAMPING: f64 = 0.98;
const NIRI_VIEWPORT_REDUCED_EPSILON: f64 = 0.4;
const NIRI_VIEWPORT_REDUCED_VELOCITY_EPSILON: f64 = 6.0;
const NIRI_VIEWPORT_MIN_REFRESH_RATE: f64 = 1.0;
const RuntimeRenderFrame = extern struct {
    window_id: abi.OmniUuid128,
    x: f64,
    y: f64,
    width: f64,
    height: f64,
};
const RuntimeAnimationState = extern struct {
    active_kind: u8,
    started_at: f64,
    duration: f64,
    last_render_count: usize,
    last_render_frames: [abi.MAX_WINDOWS]RuntimeRenderFrame,
};
const RuntimeViewportSpringState = extern struct {
    from: f64,
    to: f64,
    initial_velocity: f64,
    started_at: f64,
    response: f64,
    damping_fraction: f64,
    epsilon: f64,
    velocity_epsilon: f64,
    display_refresh_rate: f64,
};
const RuntimeViewportState = extern struct {
    active_column_index: i64,
    static_offset: f64,
    selection_progress: f64,
    gesture_active: u8,
    gesture_state: abi.OmniViewportGestureState,
    spring_active: u8,
    spring_state: RuntimeViewportSpringState,
};
pub const OmniNiriLayoutContext = extern struct {
    interaction_window_count: usize,
    interaction_windows: [abi.MAX_WINDOWS]abi.OmniNiriHitTestWindow,
    column_count: usize,
    column_dropzones: [abi.MAX_WINDOWS]abi.OmniNiriColumnDropzoneMeta,
    runtime_column_count: usize,
    runtime_columns: [abi.MAX_WINDOWS]abi.OmniNiriRuntimeColumnState,
    runtime_window_count: usize,
    runtime_windows: [abi.MAX_WINDOWS]abi.OmniNiriRuntimeWindowState,
    runtime_column_id_slots: [ID_SLOT_COUNT]i64,
    runtime_window_id_slots: [ID_SLOT_COUNT]i64,
    animation_state: RuntimeAnimationState,
    viewport_state: RuntimeViewportState,
    last_delta_generation: u64,
    last_delta_column_count: usize,
    last_delta_columns: [abi.MAX_WINDOWS]abi.OmniNiriDeltaColumnRecord,
    last_delta_window_count: usize,
    last_delta_windows: [abi.MAX_WINDOWS]abi.OmniNiriDeltaWindowRecord,
    last_delta_removed_column_count: usize,
    last_delta_removed_column_ids: [abi.MAX_WINDOWS]abi.OmniUuid128,
    last_delta_removed_window_count: usize,
    last_delta_removed_window_ids: [abi.MAX_WINDOWS]abi.OmniUuid128,
    last_delta_refresh_count: u8,
    last_delta_refresh_column_ids: [abi.OMNI_NIRI_RUNTIME_HINT_MAX_COLUMNS]abi.OmniUuid128,
    last_delta_reset_all_column_cached_widths: u8,
    last_delta_has_delegate_move_column: u8,
    last_delta_delegate_move_column_id: abi.OmniUuid128,
    last_delta_delegate_move_direction: u8,
    last_delta_has_target_window_id: u8,
    last_delta_target_window_id: abi.OmniUuid128,
    last_delta_has_target_node_id: u8,
    last_delta_target_node_kind: u8,
    last_delta_target_node_id: abi.OmniUuid128,
    last_delta_has_source_selection_window_id: u8,
    last_delta_source_selection_window_id: abi.OmniUuid128,
    last_delta_has_target_selection_window_id: u8,
    last_delta_target_selection_window_id: abi.OmniUuid128,
    last_delta_has_moved_window_id: u8,
    last_delta_moved_window_id: abi.OmniUuid128,
};
const RuntimeState = struct {
    column_count: usize,
    columns: [abi.MAX_WINDOWS]abi.OmniNiriRuntimeColumnState,
    window_count: usize,
    windows: [abi.MAX_WINDOWS]abi.OmniNiriRuntimeWindowState,
    column_id_slots: [ID_SLOT_COUNT]i64,
    window_id_slots: [ID_SLOT_COUNT]i64,
};
const MutationApplyHints = struct {
    refresh_count: usize,
    refresh_column_ids: [abi.OMNI_NIRI_RUNTIME_HINT_MAX_COLUMNS]abi.OmniUuid128,
    reset_all_column_cached_widths: bool,
    has_delegate_move_column: bool,
    delegate_move_column_id: abi.OmniUuid128,
    delegate_move_direction: u8,
};
const TxnDeltaMeta = struct {
    refresh_count: usize,
    refresh_column_ids: [abi.OMNI_NIRI_RUNTIME_HINT_MAX_COLUMNS]abi.OmniUuid128,
    reset_all_column_cached_widths: bool,
    has_delegate_move_column: bool,
    delegate_move_column_id: abi.OmniUuid128,
    delegate_move_direction: u8,
    has_target_window_id: bool,
    target_window_id: abi.OmniUuid128,
    has_target_node_id: bool,
    target_node_kind: u8,
    target_node_id: abi.OmniUuid128,
    has_source_selection_window_id: bool,
    source_selection_window_id: abi.OmniUuid128,
    has_target_selection_window_id: bool,
    target_selection_window_id: abi.OmniUuid128,
    has_moved_window_id: bool,
    moved_window_id: abi.OmniUuid128,
};
fn zeroUuid() abi.OmniUuid128 {
    return .{ .bytes = [_]u8{0} ** 16 };
}
fn zeroRuntimeRenderFrame() RuntimeRenderFrame {
    return .{
        .window_id = zeroUuid(),
        .x = 0,
        .y = 0,
        .width = 0,
        .height = 0,
    };
}
fn initRuntimeAnimationState() RuntimeAnimationState {
    return .{
        .active_kind = RUNTIME_ANIMATION_NONE,
        .started_at = 0,
        .duration = 0,
        .last_render_count = 0,
        .last_render_frames = [_]RuntimeRenderFrame{zeroRuntimeRenderFrame()} ** abi.MAX_WINDOWS,
    };
}
fn zeroViewportGestureState() abi.OmniViewportGestureState {
    return .{
        .is_trackpad = 0,
        .history_count = 0,
        .history_head = 0,
        .tracker_position = 0,
        .current_view_offset = 0,
        .stationary_view_offset = 0,
        .delta_from_tracker = 0,
        .history_deltas = [_]f64{0} ** abi.OMNI_VIEWPORT_GESTURE_HISTORY_CAP,
        .history_timestamps = [_]f64{0} ** abi.OMNI_VIEWPORT_GESTURE_HISTORY_CAP,
    };
}
fn zeroRuntimeViewportSpringState() RuntimeViewportSpringState {
    return .{
        .from = 0,
        .to = 0,
        .initial_velocity = 0,
        .started_at = 0,
        .response = NIRI_VIEWPORT_SPRING_RESPONSE,
        .damping_fraction = NIRI_VIEWPORT_SPRING_DAMPING,
        .epsilon = NIRI_VIEWPORT_SPRING_EPSILON,
        .velocity_epsilon = NIRI_VIEWPORT_SPRING_VELOCITY_EPSILON,
        .display_refresh_rate = 60,
    };
}
fn initRuntimeViewportState() RuntimeViewportState {
    return .{
        .active_column_index = 0,
        .static_offset = 0,
        .selection_progress = 0,
        .gesture_active = 0,
        .gesture_state = zeroViewportGestureState(),
        .spring_active = 0,
        .spring_state = zeroRuntimeViewportSpringState(),
    };
}
fn initMutationApplyHints() MutationApplyHints {
    return .{
        .refresh_count = 0,
        .refresh_column_ids = [_]abi.OmniUuid128{zeroUuid()} ** abi.OMNI_NIRI_RUNTIME_HINT_MAX_COLUMNS,
        .reset_all_column_cached_widths = false,
        .has_delegate_move_column = false,
        .delegate_move_column_id = zeroUuid(),
        .delegate_move_direction = 0,
    };
}
fn initTxnDeltaMeta() TxnDeltaMeta {
    return .{
        .refresh_count = 0,
        .refresh_column_ids = [_]abi.OmniUuid128{zeroUuid()} ** abi.OMNI_NIRI_RUNTIME_HINT_MAX_COLUMNS,
        .reset_all_column_cached_widths = false,
        .has_delegate_move_column = false,
        .delegate_move_column_id = zeroUuid(),
        .delegate_move_direction = 0,
        .has_target_window_id = false,
        .target_window_id = zeroUuid(),
        .has_target_node_id = false,
        .target_node_kind = abi.OMNI_NIRI_MUTATION_NODE_NONE,
        .target_node_id = zeroUuid(),
        .has_source_selection_window_id = false,
        .source_selection_window_id = zeroUuid(),
        .has_target_selection_window_id = false,
        .target_selection_window_id = zeroUuid(),
        .has_moved_window_id = false,
        .moved_window_id = zeroUuid(),
    };
}
fn resetDeltaBuffers(ctx: *OmniNiriLayoutContext) void {
    ctx.last_delta_generation = 0;
    ctx.last_delta_column_count = 0;
    ctx.last_delta_window_count = 0;
    ctx.last_delta_removed_column_count = 0;
    ctx.last_delta_removed_window_count = 0;
    ctx.last_delta_refresh_count = 0;
    ctx.last_delta_reset_all_column_cached_widths = 0;
    ctx.last_delta_has_delegate_move_column = 0;
    ctx.last_delta_delegate_move_column_id = zeroUuid();
    ctx.last_delta_delegate_move_direction = 0;
    ctx.last_delta_has_target_window_id = 0;
    ctx.last_delta_target_window_id = zeroUuid();
    ctx.last_delta_has_target_node_id = 0;
    ctx.last_delta_target_node_kind = abi.OMNI_NIRI_MUTATION_NODE_NONE;
    ctx.last_delta_target_node_id = zeroUuid();
    ctx.last_delta_has_source_selection_window_id = 0;
    ctx.last_delta_source_selection_window_id = zeroUuid();
    ctx.last_delta_has_target_selection_window_id = 0;
    ctx.last_delta_target_selection_window_id = zeroUuid();
    ctx.last_delta_has_moved_window_id = 0;
    ctx.last_delta_moved_window_id = zeroUuid();
}
fn initMutationApplyResult(out_result: [*c]abi.OmniNiriMutationApplyResult) void {
    out_result[0] = .{
        .applied = 0,
        .has_target_window_id = 0,
        .target_window_id = zeroUuid(),
        .has_target_node_id = 0,
        .target_node_kind = abi.OMNI_NIRI_MUTATION_NODE_NONE,
        .target_node_id = zeroUuid(),
        .refresh_tabbed_visibility_count = 0,
        .refresh_tabbed_visibility_column_ids = [_]abi.OmniUuid128{zeroUuid()} ** abi.OMNI_NIRI_RUNTIME_HINT_MAX_COLUMNS,
        .reset_all_column_cached_widths = 0,
        .has_delegate_move_column = 0,
        .delegate_move_column_id = zeroUuid(),
        .delegate_move_direction = 0,
    };
}
fn initWorkspaceApplyResult(out_result: [*c]abi.OmniNiriWorkspaceApplyResult) void {
    out_result[0] = .{
        .applied = 0,
        .has_source_selection_window_id = 0,
        .source_selection_window_id = zeroUuid(),
        .has_target_selection_window_id = 0,
        .target_selection_window_id = zeroUuid(),
        .has_moved_window_id = 0,
        .moved_window_id = zeroUuid(),
    };
}
fn initNavigationApplyResult(out_result: [*c]abi.OmniNiriNavigationApplyResult) void {
    out_result[0] = .{
        .applied = 0,
        .has_target_window_id = 0,
        .target_window_id = zeroUuid(),
        .update_source_active_tile = 0,
        .source_column_id = zeroUuid(),
        .source_active_tile_idx = -1,
        .update_target_active_tile = 0,
        .target_column_id = zeroUuid(),
        .target_active_tile_idx = -1,
        .refresh_tabbed_visibility_source = 0,
        .refresh_source_column_id = zeroUuid(),
        .refresh_tabbed_visibility_target = 0,
        .refresh_target_column_id = zeroUuid(),
    };
}
fn resetContext(ctx: *OmniNiriLayoutContext) void {
    ctx.interaction_window_count = 0;
    ctx.column_count = 0;
    ctx.runtime_column_count = 0;
    ctx.runtime_window_count = 0;
    for (0..ID_SLOT_COUNT) |idx| {
        ctx.runtime_column_id_slots[idx] = EMPTY_SLOT;
        ctx.runtime_window_id_slots[idx] = EMPTY_SLOT;
    }
    ctx.animation_state = initRuntimeAnimationState();
    ctx.viewport_state = initRuntimeViewportState();
    resetDeltaBuffers(ctx);
}
fn clearRuntimeAnimationState(ctx: *OmniNiriLayoutContext) void {
    ctx.animation_state.active_kind = RUNTIME_ANIMATION_NONE;
    ctx.animation_state.started_at = 0;
    ctx.animation_state.duration = 0;
}
pub fn startRuntimeAnimation(
    ctx: *OmniNiriLayoutContext,
    kind: u8,
    started_at: f64,
    duration: f64,
) void {
    ctx.animation_state.active_kind = kind;
    ctx.animation_state.started_at = started_at;
    ctx.animation_state.duration = @max(0.01, duration);
}
fn syncRuntimeAnimationState(
    ctx: *OmniNiriLayoutContext,
    sample_time: f64,
) bool {
    if (ctx.animation_state.active_kind == RUNTIME_ANIMATION_NONE) return false;
    const elapsed = @max(0.0, sample_time - ctx.animation_state.started_at);
    if (elapsed >= ctx.animation_state.duration) {
        clearRuntimeAnimationState(ctx);
        return false;
    }
    return true;
}
fn clampViewportRefreshRate(display_refresh_rate: f64) f64 {
    return @max(display_refresh_rate, NIRI_VIEWPORT_MIN_REFRESH_RATE);
}
fn viewportTargetOffset(ctx: *const OmniNiriLayoutContext) f64 {
    if (ctx.viewport_state.spring_active != 0) {
        return ctx.viewport_state.spring_state.to;
    }
    if (ctx.viewport_state.gesture_active != 0) {
        return ctx.viewport_state.gesture_state.current_view_offset;
    }
    return ctx.viewport_state.static_offset;
}
fn viewportSpringAngularFrequency(response: f64) f64 {
    if (!(response > 0)) return 0;
    return (2.0 * std.math.pi) / response;
}
fn viewportSpringDisplacement(
    spring: *const RuntimeViewportSpringState,
    sample_time: f64,
) f64 {
    const elapsed = @max(0.0, sample_time - spring.started_at);
    const omega0 = viewportSpringAngularFrequency(spring.response);
    if (!(omega0 > 0)) return 0;
    const zeta = spring.damping_fraction;
    const initial_displacement = spring.from - spring.to;
    const initial_velocity = spring.initial_velocity;
    if (zeta < 1.0) {
        const omega_d = omega0 * @sqrt(@max(0.0, 1.0 - zeta * zeta));
        if (!(omega_d > 0)) return 0;
        const exp_term = std.math.exp(-zeta * omega0 * elapsed);
        const cos_term = std.math.cos(omega_d * elapsed);
        const sin_term = std.math.sin(omega_d * elapsed);
        const b = (initial_velocity + zeta * omega0 * initial_displacement) / omega_d;
        return exp_term * (initial_displacement * cos_term + b * sin_term);
    }
    if (@abs(zeta - 1.0) <= 0.0001) {
        const exp_term = std.math.exp(-omega0 * elapsed);
        const c2 = initial_velocity + omega0 * initial_displacement;
        return exp_term * (initial_displacement + c2 * elapsed);
    }
    const omega_z = omega0 * @sqrt(@max(0.0, zeta * zeta - 1.0));
    const r1 = -omega0 * zeta + omega_z;
    const r2 = -omega0 * zeta - omega_z;
    const c2 = if (@abs(r2 - r1) <= 0.0001)
        0.0
    else
        (initial_velocity - r1 * initial_displacement) / (r2 - r1);
    const c1 = initial_displacement - c2;
    return c1 * std.math.exp(r1 * elapsed) + c2 * std.math.exp(r2 * elapsed);
}
fn viewportSpringVelocityValue(
    spring: *const RuntimeViewportSpringState,
    sample_time: f64,
) f64 {
    const elapsed = @max(0.0, sample_time - spring.started_at);
    const omega0 = viewportSpringAngularFrequency(spring.response);
    if (!(omega0 > 0)) return 0;
    const zeta = spring.damping_fraction;
    const initial_displacement = spring.from - spring.to;
    const initial_velocity = spring.initial_velocity;
    if (zeta < 1.0) {
        const omega_d = omega0 * @sqrt(@max(0.0, 1.0 - zeta * zeta));
        if (!(omega_d > 0)) return 0;
        const exp_term = std.math.exp(-zeta * omega0 * elapsed);
        const cos_term = std.math.cos(omega_d * elapsed);
        const sin_term = std.math.sin(omega_d * elapsed);
        const b = (initial_velocity + zeta * omega0 * initial_displacement) / omega_d;
        return exp_term *
            ((-zeta * omega0) * (initial_displacement * cos_term + b * sin_term) +
            (-initial_displacement * omega_d * sin_term + b * omega_d * cos_term));
    }
    if (@abs(zeta - 1.0) <= 0.0001) {
        const exp_term = std.math.exp(-omega0 * elapsed);
        const c2 = initial_velocity + omega0 * initial_displacement;
        return exp_term * (initial_velocity - omega0 * c2 * elapsed);
    }
    const omega_z = omega0 * @sqrt(@max(0.0, zeta * zeta - 1.0));
    const r1 = -omega0 * zeta + omega_z;
    const r2 = -omega0 * zeta - omega_z;
    const c2 = if (@abs(r2 - r1) <= 0.0001)
        0.0
    else
        (initial_velocity - r1 * initial_displacement) / (r2 - r1);
    const c1 = initial_displacement - c2;
    return c1 * r1 * std.math.exp(r1 * elapsed) + c2 * r2 * std.math.exp(r2 * elapsed);
}
fn viewportSpringValue(
    spring: *const RuntimeViewportSpringState,
    sample_time: f64,
) f64 {
    return spring.to + viewportSpringDisplacement(spring, sample_time);
}
fn viewportSpringIsComplete(
    spring: *const RuntimeViewportSpringState,
    sample_time: f64,
) bool {
    const position = viewportSpringValue(spring, sample_time);
    const velocity = viewportSpringVelocityValue(spring, sample_time);
    const refresh_scale = 60.0 / clampViewportRefreshRate(spring.display_refresh_rate);
    const scaled_epsilon = spring.epsilon * refresh_scale;
    const scaled_velocity_epsilon = spring.velocity_epsilon * refresh_scale;
    return @abs(position - spring.to) < scaled_epsilon and
        @abs(velocity) < scaled_velocity_epsilon;
}
fn clearViewportSpring(ctx: *OmniNiriLayoutContext) void {
    ctx.viewport_state.spring_active = 0;
    ctx.viewport_state.spring_state = zeroRuntimeViewportSpringState();
}
fn clearViewportGesture(ctx: *OmniNiriLayoutContext) void {
    ctx.viewport_state.gesture_active = 0;
    ctx.viewport_state.gesture_state = zeroViewportGestureState();
}
fn sampleViewportOffset(
    ctx: *OmniNiriLayoutContext,
    sample_time: f64,
) f64 {
    if (ctx.viewport_state.spring_active != 0) {
        if (viewportSpringIsComplete(&ctx.viewport_state.spring_state, sample_time)) {
            ctx.viewport_state.static_offset = ctx.viewport_state.spring_state.to;
            clearViewportSpring(ctx);
            return ctx.viewport_state.static_offset;
        }
        return viewportSpringValue(&ctx.viewport_state.spring_state, sample_time);
    }
    if (ctx.viewport_state.gesture_active != 0) {
        return ctx.viewport_state.gesture_state.current_view_offset;
    }
    return ctx.viewport_state.static_offset;
}
fn currentViewportVelocity(
    ctx: *OmniNiriLayoutContext,
    sample_time: f64,
) f64 {
    if (ctx.viewport_state.spring_active != 0) {
        if (viewportSpringIsComplete(&ctx.viewport_state.spring_state, sample_time)) {
            ctx.viewport_state.static_offset = ctx.viewport_state.spring_state.to;
            clearViewportSpring(ctx);
            return 0;
        }
        return viewportSpringVelocityValue(&ctx.viewport_state.spring_state, sample_time);
    }
    if (ctx.viewport_state.gesture_active != 0) {
        var velocity: f64 = 0;
        const rc = viewport.omni_viewport_gesture_velocity_impl(
            @ptrCast(&ctx.viewport_state.gesture_state),
            &velocity,
        );
        return if (rc == abi.OMNI_OK) velocity else 0;
    }
    return 0;
}
fn shiftViewportOffset(
    ctx: *OmniNiriLayoutContext,
    delta: f64,
) void {
    if (@abs(delta) <= std.math.floatEps(f64)) return;
    if (ctx.viewport_state.spring_active != 0) {
        ctx.viewport_state.spring_state.from += delta;
        ctx.viewport_state.spring_state.to += delta;
        return;
    }
    if (ctx.viewport_state.gesture_active != 0) {
        ctx.viewport_state.gesture_state.current_view_offset += delta;
        ctx.viewport_state.gesture_state.stationary_view_offset += delta;
        ctx.viewport_state.gesture_state.delta_from_tracker += delta;
        return;
    }
    ctx.viewport_state.static_offset += delta;
}
fn runtimeColumnSpanForViewport(
    column: abi.OmniNiriRuntimeColumnState,
    available_primary: f64,
    primary_gap: f64,
) ?f64 {
    const proportional_base = @max(0.0, available_primary - primary_gap);
    if (column.is_full_width != 0) {
        return available_primary;
    }
    return switch (column.width_kind) {
        abi.OMNI_NIRI_SIZE_KIND_PROPORTION => @max(0.0, proportional_base * @max(0.0, column.size_value)),
        abi.OMNI_NIRI_SIZE_KIND_FIXED => @max(0.0, column.size_value),
        else => null,
    };
}
pub fn deriveRuntimeViewportSpans(
    ctx: *const OmniNiriLayoutContext,
    primary_gap: f64,
    viewport_span: f64,
    out_spans: *[abi.MAX_WINDOWS]f64,
) i32 {
    const span_count = ctx.runtime_column_count;
    const window_count = ctx.runtime_window_count;
    if (span_count > abi.MAX_WINDOWS or window_count > abi.MAX_WINDOWS) {
        return abi.OMNI_ERR_OUT_OF_RANGE;
    }
    const available_primary = @max(0.0, viewport_span);
    for (0..span_count) |column_idx| {
        const runtime_column = ctx.runtime_columns[column_idx];
        if (runtime_column.window_start > window_count or
            runtime_column.window_count > window_count - runtime_column.window_start)
        {
            return abi.OMNI_ERR_OUT_OF_RANGE;
        }
        const span = runtimeColumnSpanForViewport(
            runtime_column,
            available_primary,
            primary_gap,
        ) orelse return abi.OMNI_ERR_INVALID_ARGS;
        out_spans[column_idx] = span;
    }
    return abi.OMNI_OK;
}
pub fn runtimeViewportActiveColumnIndex(
    ctx: *const OmniNiriLayoutContext,
    span_count: usize,
) usize {
    if (span_count == 0) return 0;
    const current = if (ctx.viewport_state.active_column_index < 0)
        0
    else
        @as(usize, @intCast(ctx.viewport_state.active_column_index));
    return @min(current, span_count - 1);
}
pub fn runtimeViewportViewStart(
    ctx: *OmniNiriLayoutContext,
    sample_time: f64,
    spans: *const [abi.MAX_WINDOWS]f64,
    span_count: usize,
    primary_gap: f64,
) f64 {
    var view_start = sampleViewportOffset(ctx, sample_time);
    const active_index = runtimeViewportActiveColumnIndex(ctx, span_count);
    for (0..active_index) |column_idx| {
        view_start += spans[column_idx] + primary_gap;
    }
    return view_start;
}
fn configureViewportSpring(
    spring: *RuntimeViewportSpringState,
    from: f64,
    to: f64,
    initial_velocity: f64,
    sample_time: f64,
    display_refresh_rate: f64,
    reduce_motion: u8,
) void {
    spring.* = .{
        .from = from,
        .to = to,
        .initial_velocity = initial_velocity,
        .started_at = sample_time,
        .response = if (reduce_motion != 0) NIRI_VIEWPORT_REDUCED_RESPONSE else NIRI_VIEWPORT_SPRING_RESPONSE,
        .damping_fraction = if (reduce_motion != 0) NIRI_VIEWPORT_REDUCED_DAMPING else NIRI_VIEWPORT_SPRING_DAMPING,
        .epsilon = if (reduce_motion != 0) NIRI_VIEWPORT_REDUCED_EPSILON else NIRI_VIEWPORT_SPRING_EPSILON,
        .velocity_epsilon = if (reduce_motion != 0) NIRI_VIEWPORT_REDUCED_VELOCITY_EPSILON else NIRI_VIEWPORT_SPRING_VELOCITY_EPSILON,
        .display_refresh_rate = clampViewportRefreshRate(display_refresh_rate),
    };
}
fn startViewportSpring(
    ctx: *OmniNiriLayoutContext,
    sample_time: f64,
    from: f64,
    to: f64,
    initial_velocity: f64,
    display_refresh_rate: f64,
    reduce_motion: u8,
) void {
    clearViewportGesture(ctx);
    ctx.viewport_state.spring_active = 1;
    ctx.viewport_state.static_offset = from;
    configureViewportSpring(
        &ctx.viewport_state.spring_state,
        from,
        to,
        initial_velocity,
        sample_time,
        display_refresh_rate,
        reduce_motion,
    );
}
fn cancelViewportMotion(
    ctx: *OmniNiriLayoutContext,
    sample_time: f64,
) void {
    ctx.viewport_state.static_offset = sampleViewportOffset(ctx, sample_time);
    clearViewportSpring(ctx);
    clearViewportGesture(ctx);
}
fn viewportAnimationActive(
    ctx: *OmniNiriLayoutContext,
    sample_time: f64,
) bool {
    _ = sampleViewportOffset(ctx, sample_time);
    return ctx.viewport_state.spring_active != 0 or ctx.viewport_state.gesture_active != 0;
}
fn rectFromWindowOutput(window: abi.OmniNiriWindowOutput) geometry.Rect {
    return .{
        .x = window.frame_x,
        .y = window.frame_y,
        .width = window.frame_width,
        .height = window.frame_height,
    };
}
fn applyAnimatedRectToWindowOutput(
    window: *allowzero abi.OmniNiriWindowOutput,
    rect: geometry.Rect,
    scale: f64,
) void {
    const rounded = geometry.roundRectToPhysicalPixels(rect, scale);
    window.animated_x = rounded.x;
    window.animated_y = rounded.y;
    window.animated_width = rounded.width;
    window.animated_height = rounded.height;
}
fn interpolateRect(from: geometry.Rect, to: geometry.Rect, progress: f64) geometry.Rect {
    const clamped = geometry.clampFloat(progress, 0.0, 1.0);
    return .{
        .x = from.x + (to.x - from.x) * clamped,
        .y = from.y + (to.y - from.y) * clamped,
        .width = from.width + (to.width - from.width) * clamped,
        .height = from.height + (to.height - from.height) * clamped,
    };
}
fn shiftedRect(rect: geometry.Rect, orientation: u8, offset: f64) geometry.Rect {
    return if (orientation == abi.OMNI_NIRI_ORIENTATION_HORIZONTAL)
        .{
            .x = rect.x + offset,
            .y = rect.y,
            .width = rect.width,
            .height = rect.height,
        }
    else
        .{
            .x = rect.x,
            .y = rect.y + offset,
            .width = rect.width,
            .height = rect.height,
        };
}
fn findLastRenderFrame(
    animation: *const RuntimeAnimationState,
    window_id: abi.OmniUuid128,
) ?RuntimeRenderFrame {
    var idx: usize = 0;
    while (idx < animation.last_render_count) : (idx += 1) {
        const frame = animation.last_render_frames[idx];
        if (uuidEqual(frame.window_id, window_id)) return frame;
    }
    return null;
}
fn snapshotRuntimeRenderFrames(
    ctx: *OmniNiriLayoutContext,
    windows: [*c]const abi.OmniNiriWindowOutput,
    window_count: usize,
) void {
    ctx.animation_state.last_render_count = @min(window_count, ctx.runtime_window_count);
    for (0..ctx.animation_state.last_render_count) |idx| {
        const output = windows[idx];
        ctx.animation_state.last_render_frames[idx] = .{
            .window_id = ctx.runtime_windows[idx].window_id,
            .x = output.animated_x,
            .y = output.animated_y,
            .width = output.animated_width,
            .height = output.animated_height,
        };
    }
}
pub fn applyRuntimeAnimationToOutputs(
    ctx: *OmniNiriLayoutContext,
    sample_time: f64,
    orientation: u8,
    scale: f64,
    windows: [*c]abi.OmniNiriWindowOutput,
    window_count: usize,
) u8 {
    const structural_active = syncRuntimeAnimationState(ctx, sample_time);
    if (!structural_active) {
        snapshotRuntimeRenderFrames(ctx, windows, window_count);
        return @intFromBool(viewportAnimationActive(ctx, sample_time));
    }
    const progress = geometry.clampFloat(
        (sample_time - ctx.animation_state.started_at) / ctx.animation_state.duration,
        0.0,
        1.0,
    );
    switch (ctx.animation_state.active_kind) {
        RUNTIME_ANIMATION_MUTATION => {
            for (0..window_count) |idx| {
                const target = rectFromWindowOutput(windows[idx]);
                const from_rect = if (findLastRenderFrame(&ctx.animation_state, ctx.runtime_windows[idx].window_id)) |frame|
                    geometry.Rect{
                        .x = frame.x,
                        .y = frame.y,
                        .width = frame.width,
                        .height = frame.height,
                    }
                else
                    shiftedRect(target, orientation, NIRI_MUTATION_APPEAR_OFFSET);
                applyAnimatedRectToWindowOutput(
                    &windows[idx],
                    interpolateRect(from_rect, target, progress),
                    scale,
                );
            }
        },
        RUNTIME_ANIMATION_WORKSPACE_SWITCH => {
            const offset = (1.0 - progress) * NIRI_WORKSPACE_SWITCH_OFFSET;
            for (0..window_count) |idx| {
                const target = rectFromWindowOutput(windows[idx]);
                applyAnimatedRectToWindowOutput(
                    &windows[idx],
                    shiftedRect(target, orientation, offset),
                    scale,
                );
            }
        },
        else => {},
    }
    snapshotRuntimeRenderFrames(ctx, windows, window_count);
    return 1;
}
pub fn omni_niri_ctx_viewport_status_impl(
    context: [*c]OmniNiriLayoutContext,
    sample_time: f64,
    out_status: [*c]abi.OmniNiriRuntimeViewportStatus,
) i32 {
    const ctx = asMutableContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    if (out_status == null) return abi.OMNI_ERR_INVALID_ARGS;
    out_status[0] = .{
        .current_offset = sampleViewportOffset(ctx, sample_time),
        .target_offset = viewportTargetOffset(ctx),
        .active_column_index = ctx.viewport_state.active_column_index,
        .selection_progress = ctx.viewport_state.selection_progress,
        .is_gesture = ctx.viewport_state.gesture_active,
        .is_animating = @intFromBool(viewportAnimationActive(ctx, sample_time)),
    };
    return abi.OMNI_OK;
}
pub fn omni_niri_ctx_viewport_begin_gesture_impl(
    context: [*c]OmniNiriLayoutContext,
    sample_time: f64,
    is_trackpad: u8,
) i32 {
    const ctx = asMutableContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    cancelViewportMotion(ctx, sample_time);
    const rc = viewport.omni_viewport_gesture_begin_impl(
        ctx.viewport_state.static_offset,
        is_trackpad,
        @ptrCast(&ctx.viewport_state.gesture_state),
    );
    if (rc != abi.OMNI_OK) return rc;
    ctx.viewport_state.gesture_active = 1;
    ctx.viewport_state.selection_progress = 0;
    return abi.OMNI_OK;
}
pub fn omni_niri_ctx_viewport_update_gesture_impl(
    context: [*c]OmniNiriLayoutContext,
    delta_pixels: f64,
    timestamp: f64,
    gap: f64,
    viewport_span: f64,
    out_result: [*c]abi.OmniViewportGestureUpdateResult,
) i32 {
    const ctx = asMutableContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    if (out_result == null) return abi.OMNI_ERR_INVALID_ARGS;
    if (ctx.viewport_state.gesture_active == 0) return abi.OMNI_ERR_INVALID_ARGS;
    const span_count = ctx.runtime_column_count;
    var spans_buf: [abi.MAX_WINDOWS]f64 = undefined;
    const spans_rc = deriveRuntimeViewportSpans(
        ctx,
        gap,
        viewport_span,
        &spans_buf,
    );
    if (spans_rc != abi.OMNI_OK) return spans_rc;
    const spans: [*c]const f64 = if (span_count > 0) @ptrCast(&spans_buf[0]) else null;
    const clamped_active_index = runtimeViewportActiveColumnIndex(ctx, span_count);
    const rc = viewport.omni_viewport_gesture_update_impl(
        @ptrCast(&ctx.viewport_state.gesture_state),
        spans,
        span_count,
        clamped_active_index,
        delta_pixels,
        timestamp,
        gap,
        viewport_span,
        ctx.viewport_state.selection_progress,
        out_result,
    );
    if (rc != abi.OMNI_OK) return rc;
    ctx.viewport_state.selection_progress = out_result[0].selection_progress;
    return abi.OMNI_OK;
}
pub fn omni_niri_ctx_viewport_end_gesture_impl(
    context: [*c]OmniNiriLayoutContext,
    gap: f64,
    viewport_span: f64,
    center_mode: u8,
    always_center_single_column: u8,
    sample_time: f64,
    display_refresh_rate: f64,
    reduce_motion: u8,
    out_result: [*c]abi.OmniViewportGestureEndResult,
) i32 {
    const ctx = asMutableContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    if (out_result == null) return abi.OMNI_ERR_INVALID_ARGS;
    if (ctx.viewport_state.gesture_active == 0) return abi.OMNI_ERR_INVALID_ARGS;
    const span_count = ctx.runtime_column_count;
    var spans_buf: [abi.MAX_WINDOWS]f64 = undefined;
    const spans_rc = deriveRuntimeViewportSpans(
        ctx,
        gap,
        viewport_span,
        &spans_buf,
    );
    if (spans_rc != abi.OMNI_OK) return spans_rc;
    const spans: [*c]const f64 = if (span_count > 0) @ptrCast(&spans_buf[0]) else null;
    const clamped_active_index = runtimeViewportActiveColumnIndex(ctx, span_count);
    const rc = viewport.omni_viewport_gesture_end_impl(
        @ptrCast(&ctx.viewport_state.gesture_state),
        spans,
        span_count,
        clamped_active_index,
        gap,
        viewport_span,
        center_mode,
        always_center_single_column,
        out_result,
    );
    if (rc != abi.OMNI_OK) return rc;
    ctx.viewport_state.active_column_index = @intCast(out_result[0].resolved_column_index);
    ctx.viewport_state.selection_progress = 0;
    startViewportSpring(
        ctx,
        sample_time,
        out_result[0].spring_from,
        out_result[0].spring_to,
        out_result[0].initial_velocity,
        display_refresh_rate,
        reduce_motion,
    );
    return abi.OMNI_OK;
}
pub fn omni_niri_ctx_viewport_transition_to_column_impl(
    context: [*c]OmniNiriLayoutContext,
    requested_index: usize,
    gap: f64,
    viewport_span: f64,
    center_mode: u8,
    always_center_single_column: u8,
    animate: u8,
    scale: f64,
    sample_time: f64,
    display_refresh_rate: f64,
    reduce_motion: u8,
    out_result: [*c]abi.OmniViewportTransitionResult,
) i32 {
    const ctx = asMutableContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    if (out_result == null) return abi.OMNI_ERR_INVALID_ARGS;
    const span_count = ctx.runtime_column_count;
    var spans_buf: [abi.MAX_WINDOWS]f64 = undefined;
    const spans_rc = deriveRuntimeViewportSpans(
        ctx,
        gap,
        viewport_span,
        &spans_buf,
    );
    if (spans_rc != abi.OMNI_OK) return spans_rc;
    const spans: [*c]const f64 = if (span_count > 0) @ptrCast(&spans_buf[0]) else null;
    if (span_count == 0) return abi.OMNI_ERR_OUT_OF_RANGE;
    const current_active_index = runtimeViewportActiveColumnIndex(ctx, span_count);
    const rc = viewport.omni_viewport_transition_to_column_impl(
        spans,
        span_count,
        current_active_index,
        requested_index,
        gap,
        viewport_span,
        viewportTargetOffset(ctx),
        center_mode,
        always_center_single_column,
        ctx.viewport_state.active_column_index,
        scale,
        out_result,
    );
    if (rc != abi.OMNI_OK) return rc;
    shiftViewportOffset(ctx, out_result[0].offset_delta);
    ctx.viewport_state.active_column_index = @intCast(out_result[0].resolved_column_index);
    if (out_result[0].snap_to_target_immediately != 0) {
        shiftViewportOffset(ctx, out_result[0].snap_delta);
        return abi.OMNI_OK;
    }
    if (animate != 0) {
        const current_offset = sampleViewportOffset(ctx, sample_time);
        const velocity = currentViewportVelocity(ctx, sample_time);
        startViewportSpring(
            ctx,
            sample_time,
            current_offset,
            out_result[0].target_offset,
            velocity,
            display_refresh_rate,
            reduce_motion,
        );
    } else {
        cancelViewportMotion(ctx, sample_time);
        ctx.viewport_state.static_offset = out_result[0].target_offset;
    }
    return abi.OMNI_OK;
}
pub fn omni_niri_ctx_viewport_set_offset_impl(
    context: [*c]OmniNiriLayoutContext,
    offset: f64,
) i32 {
    const ctx = asMutableContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    clearViewportSpring(ctx);
    clearViewportGesture(ctx);
    ctx.viewport_state.static_offset = offset;
    return abi.OMNI_OK;
}
pub fn omni_niri_ctx_viewport_cancel_impl(
    context: [*c]OmniNiriLayoutContext,
    sample_time: f64,
) i32 {
    const ctx = asMutableContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    cancelViewportMotion(ctx, sample_time);
    ctx.viewport_state.selection_progress = 0;
    return abi.OMNI_OK;
}
fn asMutableContext(context: [*c]OmniNiriLayoutContext) ?*OmniNiriLayoutContext {
    if (context == null) return null;
    const ptr: *OmniNiriLayoutContext = @ptrCast(&context[0]);
    return ptr;
}
fn asConstContext(context: [*c]const OmniNiriLayoutContext) ?*const OmniNiriLayoutContext {
    if (context == null) return null;
    const ptr: *const OmniNiriLayoutContext = @ptrCast(&context[0]);
    return ptr;
}
fn contextHitWindowsPtr(ctx: *const OmniNiriLayoutContext) [*c]const abi.OmniNiriHitTestWindow {
    if (ctx.interaction_window_count == 0) return null;
    const ptr: *const abi.OmniNiriHitTestWindow = &ctx.interaction_windows[0];
    return @ptrCast(ptr);
}
fn runtimeColumnsStatePtr(state: *const RuntimeState) [*c]const abi.OmniNiriStateColumnInput {
    if (state.column_count == 0) return null;
    const ptr: *const abi.OmniNiriStateColumnInput = @ptrCast(&state.columns[0]);
    return @ptrCast(ptr);
}
fn runtimeWindowsStatePtr(state: *const RuntimeState) [*c]const abi.OmniNiriStateWindowInput {
    if (state.window_count == 0) return null;
    const ptr: *const abi.OmniNiriStateWindowInput = @ptrCast(&state.windows[0]);
    return @ptrCast(ptr);
}
fn clearSlots(slots: *[ID_SLOT_COUNT]i64) void {
    for (0..ID_SLOT_COUNT) |idx| {
        slots[idx] = EMPTY_SLOT;
    }
}
fn uuidEqual(a: abi.OmniUuid128, b: abi.OmniUuid128) bool {
    return std.mem.eql(u8, a.bytes[0..], b.bytes[0..]);
}
fn uuidHash(uuid: abi.OmniUuid128) u64 {
    var hash: u64 = 1469598103934665603;
    for (uuid.bytes) |byte| {
        hash ^= @as(u64, byte);
        hash *%= 1099511628211;
    }
    return hash;
}
fn slotForUuid(uuid: abi.OmniUuid128) usize {
    const hashed = uuidHash(uuid) % @as(u64, ID_SLOT_COUNT);
    return @intCast(hashed);
}
fn insertColumnIdSlot(state: *RuntimeState, column_index: usize) i32 {
    const column_id = state.columns[column_index].column_id;
    var slot = slotForUuid(column_id);
    var probe: usize = 0;
    while (probe < ID_SLOT_COUNT) : (probe += 1) {
        const raw = state.column_id_slots[slot];
        if (raw == EMPTY_SLOT) {
            state.column_id_slots[slot] = std.math.cast(i64, column_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            return abi.OMNI_OK;
        }
        const existing_index = std.math.cast(usize, raw) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        if (existing_index >= state.column_count) return abi.OMNI_ERR_OUT_OF_RANGE;
        if (uuidEqual(state.columns[existing_index].column_id, column_id)) return abi.OMNI_ERR_INVALID_ARGS;
        slot = (slot + 1) % ID_SLOT_COUNT;
    }
    return abi.OMNI_ERR_OUT_OF_RANGE;
}
fn insertWindowIdSlot(state: *RuntimeState, window_index: usize) i32 {
    const window_id = state.windows[window_index].window_id;
    var slot = slotForUuid(window_id);
    var probe: usize = 0;
    while (probe < ID_SLOT_COUNT) : (probe += 1) {
        const raw = state.window_id_slots[slot];
        if (raw == EMPTY_SLOT) {
            state.window_id_slots[slot] = std.math.cast(i64, window_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            return abi.OMNI_OK;
        }
        const existing_index = std.math.cast(usize, raw) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        if (existing_index >= state.window_count) return abi.OMNI_ERR_OUT_OF_RANGE;
        if (uuidEqual(state.windows[existing_index].window_id, window_id)) return abi.OMNI_ERR_INVALID_ARGS;
        slot = (slot + 1) % ID_SLOT_COUNT;
    }
    return abi.OMNI_ERR_OUT_OF_RANGE;
}
fn rebuildRuntimeIdCaches(state: *RuntimeState) i32 {
    clearSlots(&state.column_id_slots);
    clearSlots(&state.window_id_slots);
    for (0..state.column_count) |idx| {
        const rc = insertColumnIdSlot(state, idx);
        if (rc != abi.OMNI_OK) return rc;
    }
    for (0..state.window_count) |idx| {
        const rc = insertWindowIdSlot(state, idx);
        if (rc != abi.OMNI_OK) return rc;
    }
    return abi.OMNI_OK;
}
fn findColumnIndexById(state: *const RuntimeState, column_id: abi.OmniUuid128) ?usize {
    if (state.column_count == 0) return null;
    var slot = slotForUuid(column_id);
    var probe: usize = 0;
    while (probe < ID_SLOT_COUNT) : (probe += 1) {
        const raw = state.column_id_slots[slot];
        if (raw == EMPTY_SLOT) return null;
        const idx = std.math.cast(usize, raw) orelse return null;
        if (idx < state.column_count and uuidEqual(state.columns[idx].column_id, column_id)) {
            return idx;
        }
        slot = (slot + 1) % ID_SLOT_COUNT;
    }
    return null;
}
fn findColumnIndexByIdLinear(state: *const RuntimeState, column_id: abi.OmniUuid128) ?usize {
    for (0..state.column_count) |idx| {
        if (uuidEqual(state.columns[idx].column_id, column_id)) return idx;
    }
    return null;
}
fn findColumnIndexByIdCoherent(state: *const RuntimeState, column_id: abi.OmniUuid128) ?usize {
    return findColumnIndexById(state, column_id) orelse findColumnIndexByIdLinear(state, column_id);
}
fn findWindowIndexById(state: *const RuntimeState, window_id: abi.OmniUuid128) ?usize {
    if (state.window_count == 0) return null;
    var slot = slotForUuid(window_id);
    var probe: usize = 0;
    while (probe < ID_SLOT_COUNT) : (probe += 1) {
        const raw = state.window_id_slots[slot];
        if (raw == EMPTY_SLOT) return null;
        const idx = std.math.cast(usize, raw) orelse return null;
        if (idx < state.window_count and uuidEqual(state.windows[idx].window_id, window_id)) {
            return idx;
        }
        slot = (slot + 1) % ID_SLOT_COUNT;
    }
    return null;
}
fn findWindowIndexByIdLinear(state: *const RuntimeState, window_id: abi.OmniUuid128) ?usize {
    for (0..state.window_count) |idx| {
        if (uuidEqual(state.windows[idx].window_id, window_id)) return idx;
    }
    return null;
}
fn findWindowIndexByIdCoherent(state: *const RuntimeState, window_id: abi.OmniUuid128) ?usize {
    return findWindowIndexById(state, window_id) orelse findWindowIndexByIdLinear(state, window_id);
}
fn runtimeStateFromContext(ctx: *const OmniNiriLayoutContext) RuntimeState {
    return .{
        .column_count = ctx.runtime_column_count,
        .columns = ctx.runtime_columns,
        .window_count = ctx.runtime_window_count,
        .windows = ctx.runtime_windows,
        .column_id_slots = ctx.runtime_column_id_slots,
        .window_id_slots = ctx.runtime_window_id_slots,
    };
}
fn commitRuntimeState(ctx: *OmniNiriLayoutContext, state: *const RuntimeState) void {
    ctx.runtime_column_count = state.column_count;
    ctx.runtime_columns = state.columns;
    ctx.runtime_window_count = state.window_count;
    ctx.runtime_windows = state.windows;
    ctx.runtime_column_id_slots = state.column_id_slots;
    ctx.runtime_window_id_slots = state.window_id_slots;
}
fn initTxnResult(out_result: [*c]abi.OmniNiriTxnResult) void {
    out_result[0] = .{
        .applied = 0,
        .kind = 0,
        .structural_animation_active = 0,
        .has_target_window_id = 0,
        .target_window_id = zeroUuid(),
        .has_target_node_id = 0,
        .target_node_kind = abi.OMNI_NIRI_MUTATION_NODE_NONE,
        .target_node_id = zeroUuid(),
        .changed_source_context = 0,
        .changed_target_context = 0,
        .error_code = abi.OMNI_OK,
        .delta_column_count = 0,
        .delta_window_count = 0,
        .removed_column_count = 0,
        .removed_window_count = 0,
    };
}
fn storeTxnDeltaForContext(
    ctx: *OmniNiriLayoutContext,
    pre_state: ?*const RuntimeState,
    meta: *const TxnDeltaMeta,
) i32 {
    var post_state = runtimeStateFromContext(ctx);
    ctx.last_delta_generation +%= 1;
    ctx.last_delta_column_count = post_state.column_count;
    ctx.last_delta_window_count = post_state.window_count;
    ctx.last_delta_removed_column_count = 0;
    ctx.last_delta_removed_window_count = 0;
    for (0..post_state.column_count) |idx| {
        const column = post_state.columns[idx];
        ctx.last_delta_columns[idx] = .{
            .column_id = column.column_id,
            .order_index = idx,
            .window_start = column.window_start,
            .window_count = column.window_count,
            .active_tile_idx = column.active_tile_idx,
            .is_tabbed = column.is_tabbed,
            .size_value = column.size_value,
            .width_kind = column.width_kind,
            .is_full_width = column.is_full_width,
            .has_saved_width = column.has_saved_width,
            .saved_width_kind = column.saved_width_kind,
            .saved_width_value = column.saved_width_value,
        };
    }
    for (0..post_state.window_count) |idx| {
        const window = post_state.windows[idx];
        var row_index: usize = 0;
        if (window.column_index < post_state.column_count) {
            const column = post_state.columns[window.column_index];
            if (idx >= column.window_start and idx < column.window_start + column.window_count) {
                row_index = idx - column.window_start;
            }
        }
        ctx.last_delta_windows[idx] = .{
            .window_id = window.window_id,
            .column_id = window.column_id,
            .column_order_index = window.column_index,
            .row_index = row_index,
            .size_value = window.size_value,
            .height_kind = window.height_kind,
            .height_value = window.height_value,
        };
    }
    if (pre_state) |before| {
        for (0..before.column_count) |idx| {
            const column_id = before.columns[idx].column_id;
            if (findColumnIndexById(&post_state, column_id) == null) {
                if (ctx.last_delta_removed_column_count >= abi.MAX_WINDOWS) return abi.OMNI_ERR_OUT_OF_RANGE;
                ctx.last_delta_removed_column_ids[ctx.last_delta_removed_column_count] = column_id;
                ctx.last_delta_removed_column_count += 1;
            }
        }
        for (0..before.window_count) |idx| {
            const window_id = before.windows[idx].window_id;
            if (findWindowIndexById(&post_state, window_id) == null) {
                if (ctx.last_delta_removed_window_count >= abi.MAX_WINDOWS) return abi.OMNI_ERR_OUT_OF_RANGE;
                ctx.last_delta_removed_window_ids[ctx.last_delta_removed_window_count] = window_id;
                ctx.last_delta_removed_window_count += 1;
            }
        }
    }
    ctx.last_delta_refresh_count = std.math.cast(u8, @min(meta.refresh_count, abi.OMNI_NIRI_RUNTIME_HINT_MAX_COLUMNS)) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
    const refresh_count: usize = @intCast(ctx.last_delta_refresh_count);
    for (0..refresh_count) |idx| {
        ctx.last_delta_refresh_column_ids[idx] = meta.refresh_column_ids[idx];
    }
    ctx.last_delta_reset_all_column_cached_widths = @intFromBool(meta.reset_all_column_cached_widths);
    ctx.last_delta_has_delegate_move_column = @intFromBool(meta.has_delegate_move_column);
    ctx.last_delta_delegate_move_column_id = meta.delegate_move_column_id;
    ctx.last_delta_delegate_move_direction = meta.delegate_move_direction;
    ctx.last_delta_has_target_window_id = @intFromBool(meta.has_target_window_id);
    ctx.last_delta_target_window_id = meta.target_window_id;
    ctx.last_delta_has_target_node_id = @intFromBool(meta.has_target_node_id);
    ctx.last_delta_target_node_kind = meta.target_node_kind;
    ctx.last_delta_target_node_id = meta.target_node_id;
    ctx.last_delta_has_source_selection_window_id = @intFromBool(meta.has_source_selection_window_id);
    ctx.last_delta_source_selection_window_id = meta.source_selection_window_id;
    ctx.last_delta_has_target_selection_window_id = @intFromBool(meta.has_target_selection_window_id);
    ctx.last_delta_target_selection_window_id = meta.target_selection_window_id;
    ctx.last_delta_has_moved_window_id = @intFromBool(meta.has_moved_window_id);
    ctx.last_delta_moved_window_id = meta.moved_window_id;
    return abi.OMNI_OK;
}
fn validateRuntimeState(state: *RuntimeState) i32 {
    var validation = abi.OmniNiriStateValidationResult{
        .column_count = 0,
        .window_count = 0,
        .first_invalid_column_index = -1,
        .first_invalid_window_index = -1,
        .first_error_code = abi.OMNI_OK,
    };
    return state_validation.omni_niri_validate_state_snapshot_impl(
        runtimeColumnsStatePtr(state),
        state.column_count,
        runtimeWindowsStatePtr(state),
        state.window_count,
        &validation,
    );
}
fn recomputeRuntimeTopology(state: *RuntimeState) i32 {
    if (state.column_count > abi.MAX_WINDOWS or state.window_count > abi.MAX_WINDOWS) {
        return abi.OMNI_ERR_OUT_OF_RANGE;
    }
    var cursor: usize = 0;
    for (0..state.column_count) |column_idx| {
        var column = &state.columns[column_idx];
        if (column.window_count > state.window_count - cursor) {
            return abi.OMNI_ERR_OUT_OF_RANGE;
        }
        column.window_start = cursor;
        if (column.window_count == 0) {
            column.active_tile_idx = 0;
        } else if (column.active_tile_idx >= column.window_count) {
            column.active_tile_idx = column.window_count - 1;
        }
        for (0..column.window_count) |row_idx| {
            const window_idx = cursor + row_idx;
            state.windows[window_idx].column_index = column_idx;
            state.windows[window_idx].column_id = column.column_id;
        }
        cursor += column.window_count;
    }
    if (cursor != state.window_count) return abi.OMNI_ERR_INVALID_ARGS;
    return abi.OMNI_OK;
}
fn refreshRuntimeState(state: *RuntimeState) i32 {
    const topology_rc = recomputeRuntimeTopology(state);
    if (topology_rc != abi.OMNI_OK) return topology_rc;
    const validation_rc = validateRuntimeState(state);
    if (validation_rc != abi.OMNI_OK) return validation_rc;
    return rebuildRuntimeIdCaches(state);
}
fn refreshRuntimeStateFast(state: *RuntimeState) i32 {
    const topology_rc = recomputeRuntimeTopology(state);
    if (topology_rc != abi.OMNI_OK) return topology_rc;
    return rebuildRuntimeIdCaches(state);
}
fn removeWindowAt(state: *RuntimeState, index: usize) abi.OmniNiriRuntimeWindowState {
    const removed = state.windows[index];
    var cursor = index;
    while (cursor + 1 < state.window_count) : (cursor += 1) {
        state.windows[cursor] = state.windows[cursor + 1];
    }
    state.window_count -= 1;
    return removed;
}
fn insertWindowAt(state: *RuntimeState, index: usize, window: abi.OmniNiriRuntimeWindowState) i32 {
    if (state.window_count >= abi.MAX_WINDOWS) return abi.OMNI_ERR_OUT_OF_RANGE;
    if (index > state.window_count) return abi.OMNI_ERR_OUT_OF_RANGE;
    var cursor = state.window_count;
    while (cursor > index) : (cursor -= 1) {
        state.windows[cursor] = state.windows[cursor - 1];
    }
    state.windows[index] = window;
    state.window_count += 1;
    return abi.OMNI_OK;
}
fn removeColumnAt(state: *RuntimeState, index: usize) abi.OmniNiriRuntimeColumnState {
    const removed = state.columns[index];
    var cursor = index;
    while (cursor + 1 < state.column_count) : (cursor += 1) {
        state.columns[cursor] = state.columns[cursor + 1];
    }
    state.column_count -= 1;
    return removed;
}
fn insertColumnAt(state: *RuntimeState, index: usize, column: abi.OmniNiriRuntimeColumnState) i32 {
    if (state.column_count >= abi.MAX_WINDOWS) return abi.OMNI_ERR_OUT_OF_RANGE;
    if (index > state.column_count) return abi.OMNI_ERR_OUT_OF_RANGE;
    var cursor = state.column_count;
    while (cursor > index) : (cursor -= 1) {
        state.columns[cursor] = state.columns[cursor - 1];
    }
    state.columns[index] = column;
    state.column_count += 1;
    return abi.OMNI_OK;
}
fn removeWindowRange(
    state: *RuntimeState,
    start_index: usize,
    count: usize,
    out_removed: *[abi.MAX_WINDOWS]abi.OmniNiriRuntimeWindowState,
) i32 {
    if (count == 0) return abi.OMNI_OK;
    if (start_index > state.window_count) return abi.OMNI_ERR_OUT_OF_RANGE;
    if (count > state.window_count - start_index) return abi.OMNI_ERR_OUT_OF_RANGE;
    for (0..count) |idx| {
        out_removed[idx] = state.windows[start_index + idx];
    }
    var cursor = start_index;
    while (cursor + count < state.window_count) : (cursor += 1) {
        state.windows[cursor] = state.windows[cursor + count];
    }
    state.window_count -= count;
    return abi.OMNI_OK;
}
fn appendWindowBatch(
    state: *RuntimeState,
    windows: *const [abi.MAX_WINDOWS]abi.OmniNiriRuntimeWindowState,
    count: usize,
) i32 {
    if (state.window_count > abi.MAX_WINDOWS - count) return abi.OMNI_ERR_OUT_OF_RANGE;
    for (0..count) |idx| {
        state.windows[state.window_count + idx] = windows[idx];
    }
    state.window_count += count;
    return abi.OMNI_OK;
}
fn clampSizeValue(value: f64) f64 {
    return @max(0.5, @min(2.0, value));
}
fn visibleCountFromRaw(raw_count: i64) i32 {
    const count = std.math.cast(usize, raw_count) orelse return abi.OMNI_ERR_INVALID_ARGS;
    if (count == 0) return abi.OMNI_ERR_INVALID_ARGS;
    return std.math.cast(i32, count) orelse abi.OMNI_ERR_OUT_OF_RANGE;
}
fn proportionalSizeForVisibleCount(raw_count: i64) i32 {
    const count_i32 = visibleCountFromRaw(raw_count);
    if (count_i32 < 0) return count_i32;
    return count_i32;
}
fn columnWindowStart(state: *const RuntimeState, column_index: usize) usize {
    var start: usize = 0;
    for (0..column_index) |idx| {
        start += state.columns[idx].window_count;
    }
    return start;
}
fn preColumnId(
    ids: *const [abi.MAX_WINDOWS]abi.OmniUuid128,
    count: usize,
    raw_index: i64,
) ?abi.OmniUuid128 {
    const idx = std.math.cast(usize, raw_index) orelse return null;
    if (idx >= count) return null;
    return ids[idx];
}
fn preWindowId(
    ids: *const [abi.MAX_WINDOWS]abi.OmniUuid128,
    count: usize,
    raw_index: i64,
) ?abi.OmniUuid128 {
    const idx = std.math.cast(usize, raw_index) orelse return null;
    if (idx >= count) return null;
    return ids[idx];
}
fn capturePreIds(
    state: *const RuntimeState,
    out_column_ids: *[abi.MAX_WINDOWS]abi.OmniUuid128,
    out_window_ids: *[abi.MAX_WINDOWS]abi.OmniUuid128,
) void {
    for (0..state.column_count) |idx| {
        out_column_ids[idx] = state.columns[idx].column_id;
    }
    for (0..state.window_count) |idx| {
        out_window_ids[idx] = state.windows[idx].window_id;
    }
}
fn ensureUniqueColumnId(state: *const RuntimeState, column_id: abi.OmniUuid128) i32 {
    if (findColumnIndexByIdLinear(state, column_id) != null) return abi.OMNI_ERR_INVALID_ARGS;
    return abi.OMNI_OK;
}
fn ensureUniqueWindowId(state: *const RuntimeState, window_id: abi.OmniUuid128) i32 {
    if (findWindowIndexByIdLinear(state, window_id) != null) return abi.OMNI_ERR_INVALID_ARGS;
    return abi.OMNI_OK;
}
fn appendRefreshHint(hints: *MutationApplyHints, column_id: abi.OmniUuid128) void {
    var idx: usize = 0;
    while (idx < hints.refresh_count) : (idx += 1) {
        if (uuidEqual(hints.refresh_column_ids[idx], column_id)) return;
    }
    if (hints.refresh_count >= abi.OMNI_NIRI_RUNTIME_HINT_MAX_COLUMNS) return;
    hints.refresh_column_ids[hints.refresh_count] = column_id;
    hints.refresh_count += 1;
}
fn i64ToU8(raw: i64) i32 {
    const value = std.math.cast(u8, raw) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
    return std.math.cast(i32, value) orelse abi.OMNI_ERR_OUT_OF_RANGE;
}
fn workspaceFail(code: i32, tag: []const u8) i32 {
    _ = tag;
    return code;
}
fn applyMutationEdit(
    state: *RuntimeState,
    apply_request: abi.OmniNiriMutationApplyRequest,
    edit: abi.OmniNiriMutationEdit,
    pre_column_ids: *const [abi.MAX_WINDOWS]abi.OmniUuid128,
    pre_window_ids: *const [abi.MAX_WINDOWS]abi.OmniUuid128,
    pre_column_count: usize,
    pre_window_count: usize,
    hints: *MutationApplyHints,
) i32 {
    var mutated = false;
    var requires_refresh = false;
    switch (edit.kind) {
        abi.OMNI_NIRI_MUTATION_EDIT_SET_ACTIVE_TILE => {
            const column_id = preColumnId(pre_column_ids, pre_column_count, edit.subject_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const column_idx = findColumnIndexById(state, column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            var next_active: usize = 0;
            if (edit.value_a >= 0) {
                next_active = std.math.cast(usize, edit.value_a) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            }
            state.columns[column_idx].active_tile_idx = next_active;
            mutated = true;
        },
        abi.OMNI_NIRI_MUTATION_EDIT_SWAP_WINDOWS => {
            const lhs_window_id = preWindowId(pre_window_ids, pre_window_count, edit.subject_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const rhs_window_id = preWindowId(pre_window_ids, pre_window_count, edit.related_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const lhs_idx = findWindowIndexById(state, lhs_window_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const rhs_idx = findWindowIndexById(state, rhs_window_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const temp = state.windows[lhs_idx];
            state.windows[lhs_idx] = state.windows[rhs_idx];
            state.windows[rhs_idx] = temp;
            mutated = true;
            requires_refresh = true;
        },
        abi.OMNI_NIRI_MUTATION_EDIT_MOVE_WINDOW_TO_COLUMN_INDEX => {
            const moving_window_id = preWindowId(pre_window_ids, pre_window_count, edit.subject_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const target_column_id = preColumnId(pre_column_ids, pre_column_count, edit.related_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const moving_idx = findWindowIndexById(state, moving_window_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const source_column_idx = state.windows[moving_idx].column_index;
            if (source_column_idx >= state.column_count) return abi.OMNI_ERR_OUT_OF_RANGE;
            const target_column_idx = findColumnIndexById(state, target_column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const target_column = state.columns[target_column_idx];
            const source_column = state.columns[source_column_idx];
            var insert_row: usize = 0;
            if (edit.value_a >= 0) {
                const raw_row = std.math.cast(usize, edit.value_a) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
                insert_row = @min(raw_row, target_column.window_count);
            }
            var target_abs = target_column.window_start + insert_row;
            if (source_column_idx == target_column_idx) {
                if (moving_idx < target_abs and target_abs > 0) {
                    target_abs -= 1;
                }
            } else if (source_column_idx < target_column_idx and target_abs > 0) {
                target_abs -= 1;
            }
            const moved = removeWindowAt(state, moving_idx);
            if (source_column.window_count == 0) return abi.OMNI_ERR_OUT_OF_RANGE;
            state.columns[source_column_idx].window_count -= 1;
            const insert_rc = insertWindowAt(state, target_abs, moved);
            if (insert_rc != abi.OMNI_OK) return insert_rc;
            state.columns[target_column_idx].window_count += 1;
            mutated = true;
            requires_refresh = true;
        },
        abi.OMNI_NIRI_MUTATION_EDIT_SWAP_COLUMN_WIDTH_STATE => {
            const lhs_column_id = preColumnId(pre_column_ids, pre_column_count, edit.subject_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const rhs_column_id = preColumnId(pre_column_ids, pre_column_count, edit.related_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const lhs_idx = findColumnIndexById(state, lhs_column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const rhs_idx = findColumnIndexById(state, rhs_column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const temp = state.columns[lhs_idx];
            state.columns[lhs_idx].size_value = state.columns[rhs_idx].size_value;
            state.columns[lhs_idx].width_kind = state.columns[rhs_idx].width_kind;
            state.columns[lhs_idx].is_full_width = state.columns[rhs_idx].is_full_width;
            state.columns[lhs_idx].has_saved_width = state.columns[rhs_idx].has_saved_width;
            state.columns[lhs_idx].saved_width_kind = state.columns[rhs_idx].saved_width_kind;
            state.columns[lhs_idx].saved_width_value = state.columns[rhs_idx].saved_width_value;
            state.columns[rhs_idx].size_value = temp.size_value;
            state.columns[rhs_idx].width_kind = temp.width_kind;
            state.columns[rhs_idx].is_full_width = temp.is_full_width;
            state.columns[rhs_idx].has_saved_width = temp.has_saved_width;
            state.columns[rhs_idx].saved_width_kind = temp.saved_width_kind;
            state.columns[rhs_idx].saved_width_value = temp.saved_width_value;
            mutated = true;
        },
        abi.OMNI_NIRI_MUTATION_EDIT_SWAP_WINDOW_SIZE_HEIGHT => {
            const lhs_window_id = preWindowId(pre_window_ids, pre_window_count, edit.subject_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const rhs_window_id = preWindowId(pre_window_ids, pre_window_count, edit.related_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const lhs_idx = findWindowIndexById(state, lhs_window_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const rhs_idx = findWindowIndexById(state, rhs_window_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const temp = state.windows[lhs_idx];
            state.windows[lhs_idx].size_value = state.windows[rhs_idx].size_value;
            state.windows[lhs_idx].height_kind = state.windows[rhs_idx].height_kind;
            state.windows[lhs_idx].height_value = state.windows[rhs_idx].height_value;
            state.windows[rhs_idx].size_value = temp.size_value;
            state.windows[rhs_idx].height_kind = temp.height_kind;
            state.windows[rhs_idx].height_value = temp.height_value;
            mutated = true;
        },
        abi.OMNI_NIRI_MUTATION_EDIT_RESET_WINDOW_SIZE_HEIGHT => {
            const window_id = preWindowId(pre_window_ids, pre_window_count, edit.subject_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const window_idx = findWindowIndexById(state, window_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            state.windows[window_idx].size_value = 1.0;
            state.windows[window_idx].height_kind = abi.OMNI_NIRI_HEIGHT_KIND_AUTO;
            state.windows[window_idx].height_value = 1.0;
            mutated = true;
        },
        abi.OMNI_NIRI_MUTATION_EDIT_REMOVE_COLUMN_IF_EMPTY => {
            const column_id = preColumnId(pre_column_ids, pre_column_count, edit.subject_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const column_idx_opt = findColumnIndexById(state, column_id);
            if (column_idx_opt) |column_idx| {
                if (state.columns[column_idx].window_count == 0) {
                    _ = removeColumnAt(state, column_idx);
                    mutated = true;
                    requires_refresh = true;
                    if (state.column_count == 0) {
                        if (apply_request.has_placeholder_column_id == 0) return abi.OMNI_ERR_INVALID_ARGS;
                        const placeholder_id = apply_request.placeholder_column_id;
                        const unique_rc = ensureUniqueColumnId(state, placeholder_id);
                        if (unique_rc != abi.OMNI_OK) return unique_rc;
                        const add_rc = insertColumnAt(state, 0, .{
                            .column_id = placeholder_id,
                            .window_start = 0,
                            .window_count = 0,
                            .active_tile_idx = 0,
                            .is_tabbed = 0,
                            .size_value = 1.0,
                            .width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION,
                            .is_full_width = 0,
                            .has_saved_width = 0,
                            .saved_width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION,
                            .saved_width_value = 1.0,
                        });
                        if (add_rc != abi.OMNI_OK) return add_rc;
                    }
                }
            }
        },
        abi.OMNI_NIRI_MUTATION_EDIT_REFRESH_TABBED_VISIBILITY => {
            const column_id = preColumnId(pre_column_ids, pre_column_count, edit.subject_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            appendRefreshHint(hints, column_id);
        },
        abi.OMNI_NIRI_MUTATION_EDIT_DELEGATE_MOVE_COLUMN => {
            const column_id = preColumnId(pre_column_ids, pre_column_count, edit.subject_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const direction_i32 = i64ToU8(edit.value_a);
            if (direction_i32 < 0) return direction_i32;
            hints.has_delegate_move_column = true;
            hints.delegate_move_column_id = column_id;
            hints.delegate_move_direction = @intCast(direction_i32);
        },
        abi.OMNI_NIRI_MUTATION_EDIT_CREATE_COLUMN_ADJACENT_AND_MOVE_WINDOW => {
            if (apply_request.has_created_column_id == 0) return abi.OMNI_ERR_INVALID_ARGS;
            const unique_rc = ensureUniqueColumnId(state, apply_request.created_column_id);
            if (unique_rc != abi.OMNI_OK) return unique_rc;
            const moving_window_id = preWindowId(pre_window_ids, pre_window_count, edit.subject_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const source_column_id = preColumnId(pre_column_ids, pre_column_count, edit.related_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const source_column_idx_initial = findColumnIndexByIdCoherent(state, source_column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const direction_i32 = i64ToU8(edit.value_a);
            if (direction_i32 < 0) return direction_i32;
            const direction: u8 = @intCast(direction_i32);
            if (direction != abi.OMNI_NIRI_DIRECTION_LEFT and direction != abi.OMNI_NIRI_DIRECTION_RIGHT) {
                return abi.OMNI_ERR_INVALID_ARGS;
            }
            const visible_i32 = proportionalSizeForVisibleCount(edit.value_b);
            if (visible_i32 < 0) return visible_i32;
            const visible_count: usize = @intCast(visible_i32);
            const insert_index = if (direction == abi.OMNI_NIRI_DIRECTION_RIGHT)
                source_column_idx_initial + 1
            else
                source_column_idx_initial;
            const add_rc = insertColumnAt(state, insert_index, .{
                .column_id = apply_request.created_column_id,
                .window_start = 0,
                .window_count = 0,
                .active_tile_idx = 0,
                .is_tabbed = 0,
                .size_value = 1.0 / @as(f64, @floatFromInt(visible_count)),
                .width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION,
                .is_full_width = 0,
                .has_saved_width = 0,
                .saved_width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION,
                .saved_width_value = 1.0,
            });
            if (add_rc != abi.OMNI_OK) return add_rc;
            const moving_idx = findWindowIndexByIdCoherent(state, moving_window_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const source_column_idx = findColumnIndexByIdCoherent(state, source_column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const new_column_idx = findColumnIndexByIdCoherent(state, apply_request.created_column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const target_start = columnWindowStart(state, new_column_idx);
            var insert_abs = target_start;
            if (moving_idx < insert_abs and insert_abs > 0) insert_abs -= 1;
            const moved = removeWindowAt(state, moving_idx);
            if (state.columns[source_column_idx].window_count == 0) return abi.OMNI_ERR_OUT_OF_RANGE;
            state.columns[source_column_idx].window_count -= 1;
            const insert_rc = insertWindowAt(state, insert_abs, moved);
            if (insert_rc != abi.OMNI_OK) return insert_rc;
            state.columns[new_column_idx].window_count += 1;
            mutated = true;
            requires_refresh = true;
        },
        abi.OMNI_NIRI_MUTATION_EDIT_INSERT_NEW_COLUMN_AT_INDEX_AND_MOVE_WINDOW => {
            if (apply_request.has_created_column_id == 0) return abi.OMNI_ERR_INVALID_ARGS;
            const unique_rc = ensureUniqueColumnId(state, apply_request.created_column_id);
            if (unique_rc != abi.OMNI_OK) return unique_rc;
            const moving_window_id = preWindowId(pre_window_ids, pre_window_count, edit.subject_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const visible_i32 = proportionalSizeForVisibleCount(edit.value_a);
            if (visible_i32 < 0) return visible_i32;
            const visible_count: usize = @intCast(visible_i32);
            var insert_index: usize = 0;
            if (edit.related_index > 0) {
                const raw_index = std.math.cast(usize, edit.related_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
                insert_index = @min(raw_index, state.column_count);
            }
            const add_rc = insertColumnAt(state, insert_index, .{
                .column_id = apply_request.created_column_id,
                .window_start = 0,
                .window_count = 0,
                .active_tile_idx = 0,
                .is_tabbed = 0,
                .size_value = 1.0 / @as(f64, @floatFromInt(visible_count)),
                .width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION,
                .is_full_width = 0,
                .has_saved_width = 0,
                .saved_width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION,
                .saved_width_value = 1.0,
            });
            if (add_rc != abi.OMNI_OK) return add_rc;
            const moving_idx = findWindowIndexByIdCoherent(state, moving_window_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const source_column_idx = state.windows[moving_idx].column_index;
            if (source_column_idx >= state.column_count) return abi.OMNI_ERR_OUT_OF_RANGE;
            const new_column_idx = findColumnIndexByIdCoherent(state, apply_request.created_column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const target_start = columnWindowStart(state, new_column_idx);
            var insert_abs = target_start;
            if (moving_idx < insert_abs and insert_abs > 0) insert_abs -= 1;
            const moved = removeWindowAt(state, moving_idx);
            if (state.columns[source_column_idx].window_count == 0) return abi.OMNI_ERR_OUT_OF_RANGE;
            state.columns[source_column_idx].window_count -= 1;
            const insert_rc = insertWindowAt(state, insert_abs, moved);
            if (insert_rc != abi.OMNI_OK) return insert_rc;
            state.columns[new_column_idx].window_count += 1;
            mutated = true;
            requires_refresh = true;
        },
        abi.OMNI_NIRI_MUTATION_EDIT_SWAP_COLUMNS => {
            const lhs_column_id = preColumnId(pre_column_ids, pre_column_count, edit.subject_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const rhs_column_id = preColumnId(pre_column_ids, pre_column_count, edit.related_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const lhs_idx = findColumnIndexById(state, lhs_column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const rhs_idx = findColumnIndexById(state, rhs_column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            if (lhs_idx == rhs_idx) {
            } else {
                const old = state.*;
                const temp = state.columns[lhs_idx];
                state.columns[lhs_idx] = state.columns[rhs_idx];
                state.columns[rhs_idx] = temp;
                var dst_cursor: usize = 0;
                for (0..state.column_count) |column_idx| {
                    const column_id = state.columns[column_idx].column_id;
                    var old_index_opt: ?usize = null;
                    for (0..old.column_count) |old_idx| {
                        if (uuidEqual(old.columns[old_idx].column_id, column_id)) {
                            old_index_opt = old_idx;
                            break;
                        }
                    }
                    const old_index = old_index_opt orelse return abi.OMNI_ERR_INVALID_ARGS;
                    const old_column = old.columns[old_index];
                    for (0..old_column.window_count) |row_idx| {
                        state.windows[dst_cursor + row_idx] = old.windows[old_column.window_start + row_idx];
                    }
                    dst_cursor += old_column.window_count;
                }
                mutated = true;
                requires_refresh = true;
            }
        },
        abi.OMNI_NIRI_MUTATION_EDIT_NORMALIZE_COLUMNS_BY_FACTOR => {
            if (edit.scalar_a <= 0) return abi.OMNI_ERR_INVALID_ARGS;
            for (0..state.column_count) |idx| {
                state.columns[idx].size_value = clampSizeValue(state.columns[idx].size_value * edit.scalar_a);
                state.columns[idx].width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION;
            }
            mutated = true;
        },
        abi.OMNI_NIRI_MUTATION_EDIT_NORMALIZE_COLUMN_WINDOWS_BY_FACTOR => {
            if (edit.scalar_a <= 0) return abi.OMNI_ERR_INVALID_ARGS;
            const column_id = preColumnId(pre_column_ids, pre_column_count, edit.subject_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const column_idx = findColumnIndexById(state, column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const column = state.columns[column_idx];
            for (0..column.window_count) |row_idx| {
                const window_idx = column.window_start + row_idx;
                state.windows[window_idx].size_value = clampSizeValue(state.windows[window_idx].size_value * edit.scalar_a);
                state.windows[window_idx].height_kind = abi.OMNI_NIRI_HEIGHT_KIND_AUTO;
                state.windows[window_idx].height_value = state.windows[window_idx].size_value;
            }
            mutated = true;
        },
        abi.OMNI_NIRI_MUTATION_EDIT_BALANCE_COLUMNS => {
            if (edit.scalar_a <= 0) return abi.OMNI_ERR_INVALID_ARGS;
            for (0..state.column_count) |col_idx| {
                state.columns[col_idx].size_value = edit.scalar_a;
                state.columns[col_idx].width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION;
                state.columns[col_idx].is_full_width = 0;
                state.columns[col_idx].has_saved_width = 0;
                state.columns[col_idx].saved_width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION;
                state.columns[col_idx].saved_width_value = 1.0;
                const column = state.columns[col_idx];
                for (0..column.window_count) |row_idx| {
                    const window_idx = column.window_start + row_idx;
                    state.windows[window_idx].size_value = 1.0;
                    state.windows[window_idx].height_kind = abi.OMNI_NIRI_HEIGHT_KIND_AUTO;
                    state.windows[window_idx].height_value = 1.0;
                }
            }
            mutated = true;
        },
        abi.OMNI_NIRI_MUTATION_EDIT_INSERT_INCOMING_WINDOW_INTO_COLUMN => {
            if (apply_request.has_incoming_window_id == 0) return abi.OMNI_ERR_INVALID_ARGS;
            const unique_rc = ensureUniqueWindowId(state, apply_request.incoming_window_id);
            if (unique_rc != abi.OMNI_OK) return unique_rc;
            const target_column_id = preColumnId(pre_column_ids, pre_column_count, edit.subject_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const target_column_idx = findColumnIndexById(state, target_column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const target_column = state.columns[target_column_idx];
            const visible_i32 = proportionalSizeForVisibleCount(edit.value_a);
            if (visible_i32 < 0) return visible_i32;
            const visible_count: usize = @intCast(visible_i32);
            state.columns[target_column_idx].size_value = 1.0 / @as(f64, @floatFromInt(visible_count));
            state.columns[target_column_idx].width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION;
            const insert_abs = target_column.window_start + target_column.window_count;
            const insert_rc = insertWindowAt(state, insert_abs, .{
                .window_id = apply_request.incoming_window_id,
                .column_id = target_column.column_id,
                .column_index = target_column_idx,
                .size_value = 1.0,
                .height_kind = abi.OMNI_NIRI_HEIGHT_KIND_AUTO,
                .height_value = 1.0,
            });
            if (insert_rc != abi.OMNI_OK) return insert_rc;
            state.columns[target_column_idx].window_count += 1;
            mutated = true;
            requires_refresh = true;
        },
        abi.OMNI_NIRI_MUTATION_EDIT_INSERT_INCOMING_WINDOW_IN_NEW_COLUMN => {
            if (apply_request.has_incoming_window_id == 0) return abi.OMNI_ERR_INVALID_ARGS;
            if (apply_request.has_created_column_id == 0) return abi.OMNI_ERR_INVALID_ARGS;
            const unique_window_rc = ensureUniqueWindowId(state, apply_request.incoming_window_id);
            if (unique_window_rc != abi.OMNI_OK) return unique_window_rc;
            const unique_column_rc = ensureUniqueColumnId(state, apply_request.created_column_id);
            if (unique_column_rc != abi.OMNI_OK) return unique_column_rc;
            const visible_i32 = proportionalSizeForVisibleCount(edit.value_a);
            if (visible_i32 < 0) return visible_i32;
            const visible_count: usize = @intCast(visible_i32);
            var insert_index = state.column_count;
            if (edit.subject_index >= 0) {
                const reference_column_id = preColumnId(pre_column_ids, pre_column_count, edit.subject_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
                const reference_index = findColumnIndexById(state, reference_column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
                insert_index = reference_index + 1;
            }
            const add_rc = insertColumnAt(state, insert_index, .{
                .column_id = apply_request.created_column_id,
                .window_start = 0,
                .window_count = 0,
                .active_tile_idx = 0,
                .is_tabbed = 0,
                .size_value = 1.0 / @as(f64, @floatFromInt(visible_count)),
                .width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION,
                .is_full_width = 0,
                .has_saved_width = 0,
                .saved_width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION,
                .saved_width_value = 1.0,
            });
            if (add_rc != abi.OMNI_OK) return add_rc;
            const cache_rc = refreshRuntimeState(state);
            if (cache_rc != abi.OMNI_OK) return cache_rc;
            const target_idx = findColumnIndexById(state, apply_request.created_column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const target_column = state.columns[target_idx];
            const insert_abs = target_column.window_start + target_column.window_count;
            const insert_window_rc = insertWindowAt(state, insert_abs, .{
                .window_id = apply_request.incoming_window_id,
                .column_id = target_column.column_id,
                .column_index = target_idx,
                .size_value = 1.0,
                .height_kind = abi.OMNI_NIRI_HEIGHT_KIND_AUTO,
                .height_value = 1.0,
            });
            if (insert_window_rc != abi.OMNI_OK) return insert_window_rc;
            state.columns[target_idx].window_count += 1;
            mutated = true;
            requires_refresh = true;
        },
        abi.OMNI_NIRI_MUTATION_EDIT_REMOVE_WINDOW_BY_INDEX => {
            const window_id = preWindowId(pre_window_ids, pre_window_count, edit.subject_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const window_idx = findWindowIndexById(state, window_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const source_column_idx = state.windows[window_idx].column_index;
            if (source_column_idx >= state.column_count) return abi.OMNI_ERR_OUT_OF_RANGE;
            if (state.columns[source_column_idx].window_count == 0) return abi.OMNI_ERR_OUT_OF_RANGE;
            _ = removeWindowAt(state, window_idx);
            state.columns[source_column_idx].window_count -= 1;
            mutated = true;
            requires_refresh = true;
        },
        abi.OMNI_NIRI_MUTATION_EDIT_RESET_ALL_COLUMN_CACHED_WIDTHS => {
            hints.reset_all_column_cached_widths = true;
        },
        else => return abi.OMNI_ERR_INVALID_ARGS,
    }
    if (mutated and requires_refresh) return refreshRuntimeStateFast(state);
    return abi.OMNI_OK;
}
fn updateInteractionContextFromLayout(
    ctx: *OmniNiriLayoutContext,
    columns: [*c]const abi.OmniNiriColumnInput,
    column_count: usize,
    windows: [*c]const abi.OmniNiriWindowInput,
    window_count: usize,
    out_windows: [*c]const abi.OmniNiriWindowOutput,
) i32 {
    if (column_count > abi.MAX_WINDOWS or window_count > abi.MAX_WINDOWS) {
        return abi.OMNI_ERR_OUT_OF_RANGE;
    }
    if (column_count > 0 and columns == null) return abi.OMNI_ERR_INVALID_ARGS;
    if (window_count > 0 and windows == null) return abi.OMNI_ERR_INVALID_ARGS;
    if (window_count > 0 and out_windows == null) return abi.OMNI_ERR_INVALID_ARGS;
    ctx.interaction_window_count = window_count;
    ctx.column_count = column_count;
    for (0..column_count) |idx| {
        ctx.column_dropzones[idx] = .{
            .is_valid = 0,
            .min_y = 0,
            .max_y = 0,
            .post_insertion_count = 0,
        };
    }
    for (0..column_count) |column_idx| {
        const column = columns[column_idx];
        if (!geometry.isSubrangeWithinTotal(window_count, column.window_start, column.window_count)) {
            return abi.OMNI_ERR_OUT_OF_RANGE;
        }
        if (column.window_count == 0) continue;
        const first_window_idx = column.window_start;
        const last_window_idx = column.window_start + column.window_count - 1;
        const first_window = out_windows[first_window_idx];
        const last_window = out_windows[last_window_idx];
        ctx.column_dropzones[column_idx] = .{
            .is_valid = 1,
            .min_y = first_window.frame_y,
            .max_y = last_window.frame_y + last_window.frame_height,
            .post_insertion_count = column.window_count + 1,
        };
        for (0..column.window_count) |local_window_idx| {
            const global_window_idx = column.window_start + local_window_idx;
            const window_output = out_windows[global_window_idx];
            const window_input = windows[global_window_idx];
            ctx.interaction_windows[global_window_idx] = .{
                .window_index = global_window_idx,
                .column_index = column_idx,
                .frame_x = window_output.frame_x,
                .frame_y = window_output.frame_y,
                .frame_width = window_output.frame_width,
                .frame_height = window_output.frame_height,
                .is_fullscreen = @intFromBool(window_input.sizing_mode == abi.OMNI_NIRI_SIZING_FULLSCREEN),
            };
        }
    }
    return abi.OMNI_OK;
}
pub fn omni_niri_layout_context_create_impl() [*c]OmniNiriLayoutContext {
    const ctx = std.heap.c_allocator.create(OmniNiriLayoutContext) catch return null;
    ctx.* = undefined;
    resetContext(ctx);
    return @ptrCast(ctx);
}
pub fn omni_niri_layout_context_destroy_impl(context: [*c]OmniNiriLayoutContext) void {
    const ctx = asMutableContext(context) orelse return;
    std.heap.c_allocator.destroy(ctx);
}
pub fn omni_niri_layout_context_set_interaction_impl(
    context: [*c]OmniNiriLayoutContext,
    windows: [*c]const abi.OmniNiriHitTestWindow,
    window_count: usize,
    column_dropzones: [*c]const abi.OmniNiriColumnDropzoneMeta,
    column_count: usize,
) i32 {
    const ctx = asMutableContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    if (window_count > abi.MAX_WINDOWS or column_count > abi.MAX_WINDOWS) {
        return abi.OMNI_ERR_OUT_OF_RANGE;
    }
    if (window_count > 0 and windows == null) return abi.OMNI_ERR_INVALID_ARGS;
    if (column_count > 0 and column_dropzones == null) return abi.OMNI_ERR_INVALID_ARGS;
    ctx.interaction_window_count = window_count;
    ctx.column_count = column_count;
    for (0..window_count) |idx| {
        ctx.interaction_windows[idx] = windows[idx];
    }
    for (0..column_count) |idx| {
        ctx.column_dropzones[idx] = column_dropzones[idx];
    }
    return abi.OMNI_OK;
}
pub fn omni_niri_layout_pass_v3_impl(
    context: [*c]OmniNiriLayoutContext,
    columns: [*c]const abi.OmniNiriColumnInput,
    column_count: usize,
    windows: [*c]const abi.OmniNiriWindowInput,
    window_count: usize,
    working_x: f64,
    working_y: f64,
    working_width: f64,
    working_height: f64,
    view_x: f64,
    view_y: f64,
    view_width: f64,
    view_height: f64,
    fullscreen_x: f64,
    fullscreen_y: f64,
    fullscreen_width: f64,
    fullscreen_height: f64,
    primary_gap: f64,
    secondary_gap: f64,
    view_start: f64,
    viewport_span: f64,
    workspace_offset: f64,
    scale: f64,
    orientation: u8,
    out_windows: [*c]abi.OmniNiriWindowOutput,
    out_window_count: usize,
    out_columns: [*c]abi.OmniNiriColumnOutput,
    out_column_count: usize,
) i32 {
    const ctx = asMutableContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    const rc = layout_pass.omni_niri_layout_pass_v2_impl(
        columns,
        column_count,
        windows,
        window_count,
        working_x,
        working_y,
        working_width,
        working_height,
        view_x,
        view_y,
        view_width,
        view_height,
        fullscreen_x,
        fullscreen_y,
        fullscreen_width,
        fullscreen_height,
        primary_gap,
        secondary_gap,
        view_start,
        viewport_span,
        workspace_offset,
        scale,
        orientation,
        out_windows,
        out_window_count,
        out_columns,
        out_column_count,
    );
    if (rc != abi.OMNI_OK) return rc;
    return updateInteractionContextFromLayout(
        ctx,
        columns,
        column_count,
        windows,
        window_count,
        out_windows,
    );
}
pub fn omni_niri_ctx_hit_test_tiled_impl(
    context: [*c]const OmniNiriLayoutContext,
    point_x: f64,
    point_y: f64,
    out_window_index: [*c]i64,
) i32 {
    const ctx = asConstContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    return interaction.omni_niri_hit_test_tiled_impl(
        contextHitWindowsPtr(ctx),
        ctx.interaction_window_count,
        point_x,
        point_y,
        out_window_index,
    );
}
pub fn omni_niri_ctx_hit_test_resize_impl(
    context: [*c]const OmniNiriLayoutContext,
    point_x: f64,
    point_y: f64,
    threshold: f64,
    out_result: [*c]abi.OmniNiriResizeHitResult,
) i32 {
    const ctx = asConstContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    return interaction.omni_niri_hit_test_resize_impl(
        contextHitWindowsPtr(ctx),
        ctx.interaction_window_count,
        point_x,
        point_y,
        threshold,
        out_result,
    );
}
pub fn omni_niri_ctx_hit_test_move_target_impl(
    context: [*c]const OmniNiriLayoutContext,
    point_x: f64,
    point_y: f64,
    excluding_window_index: i64,
    is_insert_mode: u8,
    out_result: [*c]abi.OmniNiriMoveTargetResult,
) i32 {
    const ctx = asConstContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    return interaction.omni_niri_hit_test_move_target_impl(
        contextHitWindowsPtr(ctx),
        ctx.interaction_window_count,
        point_x,
        point_y,
        excluding_window_index,
        is_insert_mode,
        out_result,
    );
}
pub fn omni_niri_ctx_insertion_dropzone_impl(
    context: [*c]const OmniNiriLayoutContext,
    target_window_index: i64,
    gap: f64,
    insert_position: u8,
    out_result: [*c]abi.OmniNiriDropzoneResult,
) i32 {
    const ctx = asConstContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    if (out_result == null) return abi.OMNI_ERR_INVALID_ARGS;
    out_result[0] = .{
        .frame_x = 0,
        .frame_y = 0,
        .frame_width = 0,
        .frame_height = 0,
        .is_valid = 0,
    };
    const target_idx = std.math.cast(usize, target_window_index) orelse return abi.OMNI_ERR_INVALID_ARGS;
    if (target_idx >= ctx.interaction_window_count) return abi.OMNI_ERR_INVALID_ARGS;
    const target = ctx.interaction_windows[target_idx];
    if (target.column_index >= ctx.column_count) return abi.OMNI_ERR_INVALID_ARGS;
    const column_meta = ctx.column_dropzones[target.column_index];
    if (column_meta.is_valid == 0) return abi.OMNI_OK;
    var input = abi.OmniNiriDropzoneInput{
        .target_frame_x = target.frame_x,
        .target_frame_y = target.frame_y,
        .target_frame_width = target.frame_width,
        .target_frame_height = target.frame_height,
        .column_min_y = column_meta.min_y,
        .column_max_y = column_meta.max_y,
        .gap = gap,
        .insert_position = insert_position,
        .post_insertion_count = column_meta.post_insertion_count,
    };
    return interaction.omni_niri_insertion_dropzone_impl(&input, out_result);
}
pub fn omni_niri_ctx_seed_runtime_state_impl(
    context: [*c]OmniNiriLayoutContext,
    columns: [*c]const abi.OmniNiriRuntimeColumnState,
    column_count: usize,
    windows: [*c]const abi.OmniNiriRuntimeWindowState,
    window_count: usize,
) i32 {
    const ctx = asMutableContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    if (column_count > abi.MAX_WINDOWS or window_count > abi.MAX_WINDOWS) {
        return abi.OMNI_ERR_OUT_OF_RANGE;
    }
    if (column_count > 0 and columns == null) return abi.OMNI_ERR_INVALID_ARGS;
    if (window_count > 0 and windows == null) return abi.OMNI_ERR_INVALID_ARGS;
    var runtime_state: RuntimeState = undefined;
    runtime_state.column_count = column_count;
    runtime_state.window_count = window_count;
    clearSlots(&runtime_state.column_id_slots);
    clearSlots(&runtime_state.window_id_slots);
    for (0..column_count) |idx| {
        runtime_state.columns[idx] = columns[idx];
    }
    for (0..window_count) |idx| {
        runtime_state.windows[idx] = windows[idx];
    }
    const refresh_rc = refreshRuntimeState(&runtime_state);
    if (refresh_rc != abi.OMNI_OK) return refresh_rc;
    commitRuntimeState(ctx, &runtime_state);
    const meta = initTxnDeltaMeta();
    const delta_rc = storeTxnDeltaForContext(ctx, null, &meta);
    if (delta_rc != abi.OMNI_OK) return delta_rc;
    return abi.OMNI_OK;
}
pub fn omni_niri_ctx_export_runtime_state_impl(
    context: [*c]const OmniNiriLayoutContext,
    out_export: [*c]abi.OmniNiriRuntimeStateExport,
) i32 {
    const ctx = asConstContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    if (out_export == null) return abi.OMNI_ERR_INVALID_ARGS;
    out_export[0] = .{
        .columns = if (ctx.runtime_column_count > 0) @ptrCast(&ctx.runtime_columns[0]) else null,
        .column_count = ctx.runtime_column_count,
        .windows = if (ctx.runtime_window_count > 0) @ptrCast(&ctx.runtime_windows[0]) else null,
        .window_count = ctx.runtime_window_count,
    };
    return abi.OMNI_OK;
}
fn appendRefreshColumnMeta(meta: *TxnDeltaMeta, column_id: abi.OmniUuid128) void {
    var idx: usize = 0;
    while (idx < meta.refresh_count) : (idx += 1) {
        if (uuidEqual(meta.refresh_column_ids[idx], column_id)) return;
    }
    if (meta.refresh_count >= abi.OMNI_NIRI_RUNTIME_HINT_MAX_COLUMNS) return;
    meta.refresh_column_ids[meta.refresh_count] = column_id;
    meta.refresh_count += 1;
}
fn resolveOptionalWindowIndexById(
    state: *const RuntimeState,
    has_window_id: u8,
    window_id: abi.OmniUuid128,
    out_index: *i64,
) i32 {
    if (has_window_id == 0) {
        out_index.* = -1;
        return abi.OMNI_OK;
    }
    const idx = findWindowIndexByIdCoherent(state, window_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
    out_index.* = std.math.cast(i64, idx) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
    return abi.OMNI_OK;
}
fn resolveOptionalColumnIndexById(
    state: *const RuntimeState,
    has_column_id: u8,
    column_id: abi.OmniUuid128,
    out_index: *i64,
) i32 {
    if (has_column_id == 0) {
        out_index.* = -1;
        return abi.OMNI_OK;
    }
    const idx = findColumnIndexByIdCoherent(state, column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
    out_index.* = std.math.cast(i64, idx) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
    return abi.OMNI_OK;
}
fn resolveNavigationSelection(
    source_state: *const RuntimeState,
    payload: abi.OmniNiriTxnNavigationPayload,
    out_selected_window_index: *i64,
    out_selected_column_index: *i64,
    out_selected_row_index: *i64,
) i32 {
    var selected_window_index: i64 = -1;
    var selected_column_index: i64 = -1;
    var rc = resolveOptionalWindowIndexById(
        source_state,
        payload.has_source_window_id,
        payload.source_window_id,
        &selected_window_index,
    );
    if (rc != abi.OMNI_OK) return rc;
    rc = resolveOptionalColumnIndexById(
        source_state,
        payload.has_source_column_id,
        payload.source_column_id,
        &selected_column_index,
    );
    if (rc != abi.OMNI_OK) return rc;
    var selected_row_index: i64 = -1;
    if (selected_window_index >= 0) {
        const window_idx = std.math.cast(usize, selected_window_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        if (window_idx >= source_state.window_count) return abi.OMNI_ERR_OUT_OF_RANGE;
        const derived_column_idx = source_state.windows[window_idx].column_index;
        if (derived_column_idx >= source_state.column_count) return abi.OMNI_ERR_OUT_OF_RANGE;
        if (selected_column_index >= 0) {
            const selected_column_idx = std.math.cast(usize, selected_column_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            if (selected_column_idx != derived_column_idx) return abi.OMNI_ERR_OUT_OF_RANGE;
        } else {
            selected_column_index = std.math.cast(i64, derived_column_idx) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        }
        const column = source_state.columns[derived_column_idx];
        if (window_idx < column.window_start or window_idx >= column.window_start + column.window_count) {
            return abi.OMNI_ERR_OUT_OF_RANGE;
        }
        selected_row_index = std.math.cast(i64, window_idx - column.window_start) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
    } else if (selected_column_index >= 0) {
        const selected_column_idx = std.math.cast(usize, selected_column_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        if (selected_column_idx >= source_state.column_count) return abi.OMNI_ERR_OUT_OF_RANGE;
        const column = source_state.columns[selected_column_idx];
        if (column.window_count > 0) {
            selected_window_index = std.math.cast(i64, column.window_start) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            selected_row_index = 0;
        }
    }
    out_selected_window_index.* = selected_window_index;
    out_selected_column_index.* = selected_column_index;
    out_selected_row_index.* = selected_row_index;
    return abi.OMNI_OK;
}
fn resolveMutationSelectedNode(
    runtime_state: *const RuntimeState,
    has_selected_node_id: u8,
    selected_node_id: abi.OmniUuid128,
    out_kind: *u8,
    out_index: *i64,
) i32 {
    out_kind.* = abi.OMNI_NIRI_MUTATION_NODE_NONE;
    out_index.* = -1;
    if (has_selected_node_id == 0) return abi.OMNI_OK;
    if (findWindowIndexByIdCoherent(runtime_state, selected_node_id)) |window_idx| {
        out_kind.* = abi.OMNI_NIRI_MUTATION_NODE_WINDOW;
        out_index.* = std.math.cast(i64, window_idx) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        return abi.OMNI_OK;
    }
    if (findColumnIndexByIdCoherent(runtime_state, selected_node_id)) |column_idx| {
        out_kind.* = abi.OMNI_NIRI_MUTATION_NODE_COLUMN;
        out_index.* = std.math.cast(i64, column_idx) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        return abi.OMNI_OK;
    }
    return abi.OMNI_OK;
}
fn navigationOpRequiresSelection(op: u8) bool {
    return switch (op) {
        abi.OMNI_NIRI_NAV_OP_MOVE_BY_COLUMNS,
        abi.OMNI_NIRI_NAV_OP_MOVE_VERTICAL,
        abi.OMNI_NIRI_NAV_OP_FOCUS_TARGET,
        abi.OMNI_NIRI_NAV_OP_FOCUS_DOWN_OR_LEFT,
        abi.OMNI_NIRI_NAV_OP_FOCUS_UP_OR_RIGHT,
        abi.OMNI_NIRI_NAV_OP_FOCUS_WINDOW_INDEX,
        abi.OMNI_NIRI_NAV_OP_FOCUS_WINDOW_TOP,
        abi.OMNI_NIRI_NAV_OP_FOCUS_WINDOW_BOTTOM,
        => true,
        else => false,
    };
}
fn mutationOpRequiresSourceWindow(op: u8) bool {
    return switch (op) {
        abi.OMNI_NIRI_MUTATION_OP_MOVE_WINDOW_VERTICAL,
        abi.OMNI_NIRI_MUTATION_OP_SWAP_WINDOW_VERTICAL,
        abi.OMNI_NIRI_MUTATION_OP_MOVE_WINDOW_HORIZONTAL,
        abi.OMNI_NIRI_MUTATION_OP_SWAP_WINDOW_HORIZONTAL,
        abi.OMNI_NIRI_MUTATION_OP_SWAP_WINDOWS_BY_MOVE,
        abi.OMNI_NIRI_MUTATION_OP_INSERT_WINDOW_BY_MOVE,
        abi.OMNI_NIRI_MUTATION_OP_MOVE_WINDOW_TO_COLUMN,
        abi.OMNI_NIRI_MUTATION_OP_CREATE_COLUMN_AND_MOVE,
        abi.OMNI_NIRI_MUTATION_OP_INSERT_WINDOW_IN_NEW_COLUMN,
        abi.OMNI_NIRI_MUTATION_OP_CONSUME_WINDOW,
        abi.OMNI_NIRI_MUTATION_OP_EXPEL_WINDOW,
        abi.OMNI_NIRI_MUTATION_OP_REMOVE_WINDOW,
        abi.OMNI_NIRI_MUTATION_OP_FALLBACK_SELECTION_ON_REMOVAL,
        abi.OMNI_NIRI_MUTATION_OP_SET_WINDOW_HEIGHT,
        => true,
        else => false,
    };
}
fn mutationOpRequiresSourceColumn(op: u8) bool {
    return switch (op) {
        abi.OMNI_NIRI_MUTATION_OP_MOVE_COLUMN,
        abi.OMNI_NIRI_MUTATION_OP_CLEANUP_EMPTY_COLUMN,
        abi.OMNI_NIRI_MUTATION_OP_NORMALIZE_WINDOW_SIZES,
        abi.OMNI_NIRI_MUTATION_OP_SET_COLUMN_DISPLAY,
        abi.OMNI_NIRI_MUTATION_OP_SET_COLUMN_ACTIVE_TILE,
        abi.OMNI_NIRI_MUTATION_OP_SET_COLUMN_WIDTH,
        abi.OMNI_NIRI_MUTATION_OP_TOGGLE_COLUMN_FULL_WIDTH,
        => true,
        else => false,
    };
}
fn mutationOpRequiresTargetColumn(op: u8) bool {
    return switch (op) {
        abi.OMNI_NIRI_MUTATION_OP_MOVE_WINDOW_TO_COLUMN => true,
        else => false,
    };
}
fn mutationOpTriggersStructuralAnimation(op: u8) bool {
    return switch (op) {
        abi.OMNI_NIRI_MUTATION_OP_MOVE_WINDOW_VERTICAL,
        abi.OMNI_NIRI_MUTATION_OP_SWAP_WINDOW_VERTICAL,
        abi.OMNI_NIRI_MUTATION_OP_MOVE_WINDOW_HORIZONTAL,
        abi.OMNI_NIRI_MUTATION_OP_SWAP_WINDOW_HORIZONTAL,
        abi.OMNI_NIRI_MUTATION_OP_SWAP_WINDOWS_BY_MOVE,
        abi.OMNI_NIRI_MUTATION_OP_INSERT_WINDOW_BY_MOVE,
        abi.OMNI_NIRI_MUTATION_OP_MOVE_WINDOW_TO_COLUMN,
        abi.OMNI_NIRI_MUTATION_OP_CREATE_COLUMN_AND_MOVE,
        abi.OMNI_NIRI_MUTATION_OP_INSERT_WINDOW_IN_NEW_COLUMN,
        abi.OMNI_NIRI_MUTATION_OP_MOVE_COLUMN,
        abi.OMNI_NIRI_MUTATION_OP_CONSUME_WINDOW,
        abi.OMNI_NIRI_MUTATION_OP_EXPEL_WINDOW,
        abi.OMNI_NIRI_MUTATION_OP_BALANCE_SIZES,
        abi.OMNI_NIRI_MUTATION_OP_ADD_WINDOW,
        abi.OMNI_NIRI_MUTATION_OP_REMOVE_WINDOW,
        abi.OMNI_NIRI_MUTATION_OP_SET_COLUMN_DISPLAY,
        abi.OMNI_NIRI_MUTATION_OP_SET_COLUMN_WIDTH,
        abi.OMNI_NIRI_MUTATION_OP_TOGGLE_COLUMN_FULL_WIDTH,
        => true,
        else => false,
    };
}
fn applyDirectMutationTxn(
    source_ctx: *OmniNiriLayoutContext,
    runtime_state: *RuntimeState,
    payload: abi.OmniNiriTxnMutationPayload,
    source_window_index: i64,
    source_column_index: i64,
    out_result: [*c]abi.OmniNiriTxnResult,
    source_delta_meta: *TxnDeltaMeta,
) ?i32 {
    var mutated = false;
    switch (payload.op) {
        abi.OMNI_NIRI_MUTATION_OP_SET_COLUMN_DISPLAY => {
            if (source_column_index < 0) return abi.OMNI_ERR_INVALID_ARGS;
            const column_idx = std.math.cast(usize, source_column_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            if (column_idx >= runtime_state.column_count) return abi.OMNI_ERR_OUT_OF_RANGE;
            const is_tabbed: u8 = if (payload.custom_u8_a != 0) 1 else 0;
            var column = &runtime_state.columns[column_idx];
            if (column.is_tabbed != is_tabbed) {
                column.is_tabbed = is_tabbed;
                mutated = true;
            }
            const clamped_active: usize = if (column.window_count == 0)
                0
            else
                @min(column.active_tile_idx, column.window_count - 1);
            if (column.active_tile_idx != clamped_active) {
                column.active_tile_idx = clamped_active;
                mutated = true;
            }
            if (mutated) {
                appendRefreshColumnMeta(source_delta_meta, column.column_id);
            }
        },
        abi.OMNI_NIRI_MUTATION_OP_SET_COLUMN_ACTIVE_TILE => {
            if (source_column_index < 0) return abi.OMNI_ERR_INVALID_ARGS;
            const column_idx = std.math.cast(usize, source_column_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            if (column_idx >= runtime_state.column_count) return abi.OMNI_ERR_OUT_OF_RANGE;
            var column = &runtime_state.columns[column_idx];
            if (column.window_count == 0) {
                return abi.OMNI_OK;
            }
            const requested = payload.custom_i64_a;
            const requested_idx: usize = if (requested < 0)
                0
            else
                std.math.cast(usize, requested) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const clamped_active = @min(requested_idx, column.window_count - 1);
            if (column.active_tile_idx != clamped_active) {
                column.active_tile_idx = clamped_active;
                mutated = true;
                if (column.is_tabbed != 0) {
                    appendRefreshColumnMeta(source_delta_meta, column.column_id);
                }
            }
        },
        abi.OMNI_NIRI_MUTATION_OP_SET_COLUMN_WIDTH => {
            if (source_column_index < 0) return abi.OMNI_ERR_INVALID_ARGS;
            const column_idx = std.math.cast(usize, source_column_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            if (column_idx >= runtime_state.column_count) return abi.OMNI_ERR_OUT_OF_RANGE;
            const width_kind = payload.custom_u8_a;
            if (width_kind != abi.OMNI_NIRI_SIZE_KIND_PROPORTION and
                width_kind != abi.OMNI_NIRI_SIZE_KIND_FIXED)
            {
                return abi.OMNI_ERR_INVALID_ARGS;
            }
            const width_value = payload.custom_f64_a;
            if (!(std.math.isFinite(width_value)) or width_value <= 0) return abi.OMNI_ERR_INVALID_ARGS;
            var column = &runtime_state.columns[column_idx];
            if (column.width_kind != width_kind or
                column.size_value != width_value or
                column.is_full_width != 0 or
                column.has_saved_width != 0)
            {
                column.size_value = width_value;
                column.width_kind = width_kind;
                column.is_full_width = 0;
                column.has_saved_width = 0;
                column.saved_width_kind = width_kind;
                column.saved_width_value = width_value;
                mutated = true;
            }
        },
        abi.OMNI_NIRI_MUTATION_OP_TOGGLE_COLUMN_FULL_WIDTH => {
            if (source_column_index < 0) return abi.OMNI_ERR_INVALID_ARGS;
            const column_idx = std.math.cast(usize, source_column_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            if (column_idx >= runtime_state.column_count) return abi.OMNI_ERR_OUT_OF_RANGE;
            var column = &runtime_state.columns[column_idx];
            if (column.is_full_width != 0) {
                var restored_kind: u8 = abi.OMNI_NIRI_SIZE_KIND_PROPORTION;
                var restored_value: f64 = 1.0;
                if (column.has_saved_width != 0) {
                    restored_kind = column.saved_width_kind;
                    restored_value = column.saved_width_value;
                }
                column.size_value = restored_value;
                column.width_kind = restored_kind;
                column.is_full_width = 0;
                column.has_saved_width = 0;
                column.saved_width_kind = restored_kind;
                column.saved_width_value = restored_value;
                mutated = true;
            } else {
                column.is_full_width = 1;
                column.has_saved_width = 1;
                column.saved_width_kind = column.width_kind;
                column.saved_width_value = column.size_value;
                mutated = true;
            }
        },
        abi.OMNI_NIRI_MUTATION_OP_SET_WINDOW_HEIGHT => {
            if (source_window_index < 0) return abi.OMNI_ERR_INVALID_ARGS;
            const window_idx = std.math.cast(usize, source_window_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            if (window_idx >= runtime_state.window_count) return abi.OMNI_ERR_OUT_OF_RANGE;
            const height_kind = payload.custom_u8_a;
            if (height_kind != abi.OMNI_NIRI_HEIGHT_KIND_AUTO and
                height_kind != abi.OMNI_NIRI_HEIGHT_KIND_FIXED)
            {
                return abi.OMNI_ERR_INVALID_ARGS;
            }
            const height_value = payload.custom_f64_a;
            if (!(std.math.isFinite(height_value))) return abi.OMNI_ERR_INVALID_ARGS;
            const size_value: f64 = if (height_kind == abi.OMNI_NIRI_HEIGHT_KIND_AUTO)
                height_value
            else
                1.0;
            var window = &runtime_state.windows[window_idx];
            if (window.height_kind != height_kind or
                window.height_value != height_value or
                window.size_value != size_value)
            {
                window.height_kind = height_kind;
                window.height_value = height_value;
                window.size_value = size_value;
                mutated = true;
            }
        },
        abi.OMNI_NIRI_MUTATION_OP_CLEAR_WORKSPACE => {
            const placeholder_id = if (payload.has_placeholder_column_id != 0)
                payload.placeholder_column_id
            else if (runtime_state.column_count > 0)
                runtime_state.columns[0].column_id
            else
                zeroUuid();
            if (uuidEqual(placeholder_id, zeroUuid())) return abi.OMNI_ERR_INVALID_ARGS;
            const already_empty = runtime_state.window_count == 0 and
                runtime_state.column_count == 1 and
                uuidEqual(runtime_state.columns[0].column_id, placeholder_id) and
                runtime_state.columns[0].window_count == 0 and
                runtime_state.columns[0].active_tile_idx == 0 and
                runtime_state.columns[0].is_tabbed == 0 and
                runtime_state.columns[0].size_value == 1.0 and
                runtime_state.columns[0].width_kind == abi.OMNI_NIRI_SIZE_KIND_PROPORTION and
                runtime_state.columns[0].is_full_width == 0 and
                runtime_state.columns[0].has_saved_width == 0 and
                runtime_state.columns[0].saved_width_kind == abi.OMNI_NIRI_SIZE_KIND_PROPORTION and
                runtime_state.columns[0].saved_width_value == 1.0;
            if (already_empty) {
                return abi.OMNI_OK;
            }
            runtime_state.window_count = 0;
            runtime_state.column_count = 1;
            runtime_state.columns[0] = .{
                .column_id = placeholder_id,
                .window_start = 0,
                .window_count = 0,
                .active_tile_idx = 0,
                .is_tabbed = 0,
                .size_value = 1.0,
                .width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION,
                .is_full_width = 0,
                .has_saved_width = 0,
                .saved_width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION,
                .saved_width_value = 1.0,
            };
            mutated = true;
        },
        else => return null,
    }
    if (!mutated) {
        return abi.OMNI_OK;
    }
    const refresh_rc = refreshRuntimeState(runtime_state);
    if (refresh_rc != abi.OMNI_OK) return refresh_rc;
    commitRuntimeState(source_ctx, runtime_state);
    out_result[0].applied = 1;
    out_result[0].changed_source_context = 1;
    out_result[0].structural_animation_active = @intFromBool(
        mutationOpTriggersStructuralAnimation(payload.op)
    );
    return abi.OMNI_OK;
}
fn applyNavigationTxn(
    source_ctx: *OmniNiriLayoutContext,
    source_state: *RuntimeState,
    payload: abi.OmniNiriTxnNavigationPayload,
    out_result: [*c]abi.OmniNiriTxnResult,
    source_delta_meta: *TxnDeltaMeta,
) i32 {
    if (navigationOpRequiresSelection(payload.op) and
        payload.has_source_window_id == 0 and
        payload.has_source_column_id == 0)
    {
        return abi.OMNI_ERR_INVALID_ARGS;
    }
    var selected_window_index: i64 = -1;
    var selected_column_index: i64 = -1;
    var selected_row_index: i64 = -1;
    var rc = resolveNavigationSelection(
        source_state,
        payload,
        &selected_window_index,
        &selected_column_index,
        &selected_row_index,
    );
    if (rc != abi.OMNI_OK) return rc;
    var target_column_index_from_id: i64 = -1;
    rc = resolveOptionalColumnIndexById(
        source_state,
        payload.has_target_column_id,
        payload.target_column_id,
        &target_column_index_from_id,
    );
    if (rc != abi.OMNI_OK) return rc;
    var target_window_index_from_id: i64 = -1;
    rc = resolveOptionalWindowIndexById(
        source_state,
        payload.has_target_window_id,
        payload.target_window_id,
        &target_window_index_from_id,
    );
    if (rc != abi.OMNI_OK) return rc;
    const target_column_index: i64 = if (target_column_index_from_id >= 0)
        target_column_index_from_id
    else
        payload.focus_column_index;
    var target_window_index: i64 = payload.focus_window_index;
    if (target_window_index_from_id >= 0) {
        if (selected_column_index < 0) return abi.OMNI_ERR_INVALID_ARGS;
        const selected_column_idx = std.math.cast(usize, selected_column_index) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        if (selected_column_idx >= source_state.column_count) return abi.OMNI_ERR_OUT_OF_RANGE;
        const selected_column = source_state.columns[selected_column_idx];
        const target_window_idx = std.math.cast(usize, target_window_index_from_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        if (target_window_idx < selected_column.window_start or
            target_window_idx >= selected_column.window_start + selected_column.window_count)
        {
            return abi.OMNI_ERR_OUT_OF_RANGE;
        }
        target_window_index = std.math.cast(
            i64,
            target_window_idx - selected_column.window_start,
        ) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
    }
    const request: abi.OmniNiriNavigationRequest = .{
        .op = payload.op,
        .direction = payload.direction,
        .orientation = payload.orientation,
        .infinite_loop = payload.infinite_loop,
        .selected_window_index = selected_window_index,
        .selected_column_index = selected_column_index,
        .selected_row_index = selected_row_index,
        .step = payload.step,
        .target_row_index = payload.target_row_index,
        .target_column_index = target_column_index,
        .target_window_index = target_window_index,
    };
    var nav_result: abi.OmniNiriNavigationResult = undefined;
    const nav_rc = navigation.omni_niri_navigation_resolve_impl(
        runtimeColumnsStatePtr(source_state),
        source_state.column_count,
        runtimeWindowsStatePtr(source_state),
        source_state.window_count,
        &request,
        &nav_result,
    );
    if (nav_rc != abi.OMNI_OK) return nav_rc;
    var pre_column_ids: [abi.MAX_WINDOWS]abi.OmniUuid128 = undefined;
    var pre_window_ids: [abi.MAX_WINDOWS]abi.OmniUuid128 = undefined;
    capturePreIds(source_state, &pre_column_ids, &pre_window_ids);
    if (nav_result.has_target != 0) {
        const target_window_id = preWindowId(
            &pre_window_ids,
            source_state.window_count,
            nav_result.target_window_index,
        ) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        out_result[0].has_target_window_id = 1;
        out_result[0].target_window_id = target_window_id;
        source_delta_meta.has_target_window_id = true;
        source_delta_meta.target_window_id = target_window_id;
    }
    var mutated = false;
    if (nav_result.update_source_active_tile != 0) {
        const column_id = preColumnId(
            &pre_column_ids,
            source_state.column_count,
            nav_result.source_column_index,
        ) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        const column_idx = findColumnIndexById(source_state, column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        const row_idx = std.math.cast(usize, nav_result.source_active_tile_idx) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        if (source_state.columns[column_idx].active_tile_idx != row_idx) {
            source_state.columns[column_idx].active_tile_idx = row_idx;
            mutated = true;
        }
    }
    if (nav_result.update_target_active_tile != 0) {
        const column_id = preColumnId(
            &pre_column_ids,
            source_state.column_count,
            nav_result.target_column_index,
        ) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        const column_idx = findColumnIndexById(source_state, column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        const row_idx = std.math.cast(usize, nav_result.target_active_tile_idx) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        if (source_state.columns[column_idx].active_tile_idx != row_idx) {
            source_state.columns[column_idx].active_tile_idx = row_idx;
            mutated = true;
        }
    }
    if (nav_result.refresh_tabbed_visibility_source != 0) {
        const column_id = preColumnId(
            &pre_column_ids,
            source_state.column_count,
            nav_result.source_column_index,
        ) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        appendRefreshColumnMeta(source_delta_meta, column_id);
    }
    if (nav_result.refresh_tabbed_visibility_target != 0) {
        const column_id = preColumnId(
            &pre_column_ids,
            source_state.column_count,
            nav_result.target_column_index,
        ) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        appendRefreshColumnMeta(source_delta_meta, column_id);
    }
    if (mutated) {
        const refresh_rc = refreshRuntimeStateFast(source_state);
        if (refresh_rc != abi.OMNI_OK) return refresh_rc;
        const validation_rc = validateRuntimeState(source_state);
        if (validation_rc != abi.OMNI_OK) return validation_rc;
        commitRuntimeState(source_ctx, source_state);
        out_result[0].applied = 1;
        out_result[0].changed_source_context = 1;
    }
    return abi.OMNI_OK;
}
fn applyMutationTxn(
    source_ctx: *OmniNiriLayoutContext,
    runtime_state: *RuntimeState,
    payload: abi.OmniNiriTxnMutationPayload,
    out_result: [*c]abi.OmniNiriTxnResult,
    source_delta_meta: *TxnDeltaMeta,
) i32 {
    if (mutationOpRequiresSourceWindow(payload.op) and payload.has_source_window_id == 0) {
        return abi.OMNI_ERR_INVALID_ARGS;
    }
    if (mutationOpRequiresSourceColumn(payload.op) and payload.has_source_column_id == 0) {
        return abi.OMNI_ERR_INVALID_ARGS;
    }
    if (mutationOpRequiresTargetColumn(payload.op) and payload.has_target_column_id == 0) {
        return abi.OMNI_ERR_INVALID_ARGS;
    }
    var source_window_index: i64 = -1;
    var target_window_index: i64 = -1;
    var source_column_index: i64 = -1;
    var target_column_index: i64 = -1;
    var focused_window_index: i64 = -1;
    var rc = resolveOptionalWindowIndexById(
        runtime_state,
        payload.has_source_window_id,
        payload.source_window_id,
        &source_window_index,
    );
    if (rc != abi.OMNI_OK) return rc;
    rc = resolveOptionalWindowIndexById(
        runtime_state,
        payload.has_target_window_id,
        payload.target_window_id,
        &target_window_index,
    );
    if (rc != abi.OMNI_OK) return rc;
    rc = resolveOptionalColumnIndexById(
        runtime_state,
        payload.has_source_column_id,
        payload.source_column_id,
        &source_column_index,
    );
    if (rc != abi.OMNI_OK) return rc;
    rc = resolveOptionalColumnIndexById(
        runtime_state,
        payload.has_target_column_id,
        payload.target_column_id,
        &target_column_index,
    );
    if (rc != abi.OMNI_OK) return rc;
    rc = resolveOptionalWindowIndexById(
        runtime_state,
        payload.has_focused_window_id,
        payload.focused_window_id,
        &focused_window_index,
    );
    if (rc != abi.OMNI_OK) return rc;
    if (applyDirectMutationTxn(
        source_ctx,
        runtime_state,
        payload,
        source_window_index,
        source_column_index,
        out_result,
        source_delta_meta,
    )) |direct_rc| {
        return direct_rc;
    }
    var selected_node_kind: u8 = abi.OMNI_NIRI_MUTATION_NODE_NONE;
    var selected_node_index: i64 = -1;
    rc = resolveMutationSelectedNode(
        runtime_state,
        payload.has_selected_node_id,
        payload.selected_node_id,
        &selected_node_kind,
        &selected_node_index,
    );
    if (rc != abi.OMNI_OK) return rc;
    const request: abi.OmniNiriMutationRequest = .{
        .op = payload.op,
        .direction = payload.direction,
        .infinite_loop = payload.infinite_loop,
        .insert_position = payload.insert_position,
        .source_window_index = source_window_index,
        .target_window_index = target_window_index,
        .max_windows_per_column = payload.max_windows_per_column,
        .source_column_index = source_column_index,
        .target_column_index = target_column_index,
        .insert_column_index = payload.insert_column_index,
        .max_visible_columns = payload.max_visible_columns,
        .selected_node_kind = selected_node_kind,
        .selected_node_index = selected_node_index,
        .focused_window_index = focused_window_index,
        .incoming_spawn_mode = payload.incoming_spawn_mode,
    };
    const apply_request: abi.OmniNiriMutationApplyRequest = .{
        .request = request,
        .has_incoming_window_id = payload.has_incoming_window_id,
        .incoming_window_id = payload.incoming_window_id,
        .has_created_column_id = payload.has_created_column_id,
        .created_column_id = payload.created_column_id,
        .has_placeholder_column_id = payload.has_placeholder_column_id,
        .placeholder_column_id = payload.placeholder_column_id,
    };
    var plan_result: abi.OmniNiriMutationResult = undefined;
    const planner_rc = mutation.omni_niri_mutation_plan_impl(
        runtimeColumnsStatePtr(runtime_state),
        runtime_state.column_count,
        runtimeWindowsStatePtr(runtime_state),
        runtime_state.window_count,
        &apply_request.request,
        &plan_result,
    );
    if (planner_rc != abi.OMNI_OK) return workspaceFail(planner_rc, "planner_rc");
    var pre_column_ids: [abi.MAX_WINDOWS]abi.OmniUuid128 = undefined;
    var pre_window_ids: [abi.MAX_WINDOWS]abi.OmniUuid128 = undefined;
    const pre_column_count = runtime_state.column_count;
    const pre_window_count = runtime_state.window_count;
    capturePreIds(runtime_state, &pre_column_ids, &pre_window_ids);
    if (plan_result.has_target_window != 0) {
        const target_window_id = preWindowId(
            &pre_window_ids,
            pre_window_count,
            plan_result.target_window_index,
        ) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
        out_result[0].has_target_window_id = 1;
        out_result[0].target_window_id = target_window_id;
        source_delta_meta.has_target_window_id = true;
        source_delta_meta.target_window_id = target_window_id;
    }
    if (plan_result.has_target_node != 0) {
        out_result[0].has_target_node_id = 1;
        out_result[0].target_node_kind = plan_result.target_node_kind;
        switch (plan_result.target_node_kind) {
            abi.OMNI_NIRI_MUTATION_NODE_WINDOW => {
                const target_window_id = preWindowId(
                    &pre_window_ids,
                    pre_window_count,
                    plan_result.target_node_index,
                ) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
                out_result[0].target_node_id = target_window_id;
                source_delta_meta.has_target_node_id = true;
                source_delta_meta.target_node_kind = plan_result.target_node_kind;
                source_delta_meta.target_node_id = target_window_id;
            },
            abi.OMNI_NIRI_MUTATION_NODE_COLUMN => {
                const target_column_id = preColumnId(
                    &pre_column_ids,
                    pre_column_count,
                    plan_result.target_node_index,
                ) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
                out_result[0].target_node_id = target_column_id;
                source_delta_meta.has_target_node_id = true;
                source_delta_meta.target_node_kind = plan_result.target_node_kind;
                source_delta_meta.target_node_id = target_column_id;
            },
            else => return abi.OMNI_ERR_INVALID_ARGS,
        }
    }
    if (plan_result.applied == 0) {
        out_result[0].applied = 0;
        return abi.OMNI_OK;
    }
    var hints = initMutationApplyHints();
    const max_edits = @min(plan_result.edit_count, abi.OMNI_NIRI_MUTATION_MAX_EDITS);
    for (0..max_edits) |idx| {
        const apply_rc = applyMutationEdit(
            runtime_state,
            apply_request,
            plan_result.edits[idx],
            &pre_column_ids,
            &pre_window_ids,
            pre_column_count,
            pre_window_count,
            &hints,
        );
        if (apply_rc != abi.OMNI_OK) return apply_rc;
    }
    const final_validation_rc = validateRuntimeState(runtime_state);
    if (final_validation_rc != abi.OMNI_OK) return final_validation_rc;
    commitRuntimeState(source_ctx, runtime_state);
    out_result[0].applied = 1;
    out_result[0].changed_source_context = 1;
    out_result[0].structural_animation_active = @intFromBool(
        mutationOpTriggersStructuralAnimation(payload.op)
    );
    source_delta_meta.refresh_count = hints.refresh_count;
    source_delta_meta.refresh_column_ids = hints.refresh_column_ids;
    source_delta_meta.reset_all_column_cached_widths = hints.reset_all_column_cached_widths;
    source_delta_meta.has_delegate_move_column = hints.has_delegate_move_column;
    source_delta_meta.delegate_move_column_id = hints.delegate_move_column_id;
    source_delta_meta.delegate_move_direction = hints.delegate_move_direction;
    return abi.OMNI_OK;
}
fn applyWorkspaceTxn(
    source_ctx: *OmniNiriLayoutContext,
    target_ctx: *OmniNiriLayoutContext,
    source_state: *RuntimeState,
    target_state: *RuntimeState,
    payload: abi.OmniNiriTxnWorkspacePayload,
    out_result: [*c]abi.OmniNiriTxnResult,
    source_meta: *TxnDeltaMeta,
    target_meta: *TxnDeltaMeta,
) i32 {
    const target_had_no_windows_before_move = target_state.window_count == 0;
    switch (payload.op) {
        abi.OMNI_NIRI_WORKSPACE_OP_MOVE_WINDOW_TO_WORKSPACE => {
            if (payload.has_source_window_id == 0) return abi.OMNI_ERR_INVALID_ARGS;
        },
        abi.OMNI_NIRI_WORKSPACE_OP_MOVE_COLUMN_TO_WORKSPACE => {
            if (payload.has_source_column_id == 0) return abi.OMNI_ERR_INVALID_ARGS;
        },
        else => return abi.OMNI_ERR_INVALID_ARGS,
    }
    var source_window_index: i64 = -1;
    var source_column_index: i64 = -1;
    var rc = resolveOptionalWindowIndexById(
        source_state,
        payload.has_source_window_id,
        payload.source_window_id,
        &source_window_index,
    );
    if (rc != abi.OMNI_OK) return rc;
    rc = resolveOptionalColumnIndexById(
        source_state,
        payload.has_source_column_id,
        payload.source_column_id,
        &source_column_index,
    );
    if (rc != abi.OMNI_OK) return rc;
    const request: abi.OmniNiriWorkspaceRequest = .{
        .op = payload.op,
        .source_window_index = source_window_index,
        .source_column_index = source_column_index,
        .max_visible_columns = payload.max_visible_columns,
    };
    const apply_request: abi.OmniNiriWorkspaceApplyRequest = .{
        .request = request,
        .has_target_created_column_id = payload.has_target_created_column_id,
        .target_created_column_id = payload.target_created_column_id,
        .has_source_placeholder_column_id = payload.has_source_placeholder_column_id,
        .source_placeholder_column_id = payload.source_placeholder_column_id,
    };
    var plan_result: abi.OmniNiriWorkspaceResult = undefined;
    const planner_rc = workspace.omni_niri_workspace_plan_impl(
        runtimeColumnsStatePtr(source_state),
        source_state.column_count,
        runtimeWindowsStatePtr(source_state),
        source_state.window_count,
        runtimeColumnsStatePtr(target_state),
        target_state.column_count,
        runtimeWindowsStatePtr(target_state),
        target_state.window_count,
        &apply_request.request,
        &plan_result,
    );
    if (planner_rc != abi.OMNI_OK) return planner_rc;
    if (plan_result.applied == 0) return abi.OMNI_OK;
    var pre_source_column_ids: [abi.MAX_WINDOWS]abi.OmniUuid128 = undefined;
    var pre_source_window_ids: [abi.MAX_WINDOWS]abi.OmniUuid128 = undefined;
    var pre_target_column_ids: [abi.MAX_WINDOWS]abi.OmniUuid128 = undefined;
    var pre_target_window_ids: [abi.MAX_WINDOWS]abi.OmniUuid128 = undefined;
    capturePreIds(source_state, &pre_source_column_ids, &pre_source_window_ids);
    capturePreIds(target_state, &pre_target_column_ids, &pre_target_window_ids);
    var remove_source_column_ids: [abi.OMNI_NIRI_WORKSPACE_MAX_EDITS]abi.OmniUuid128 = undefined;
    var remove_source_column_count: usize = 0;
    var has_source_selection_window_id = false;
    var source_selection_window_id = zeroUuid();
    var source_selection_cleared = false;
    var has_target_selection_moved_window = false;
    var target_selection_moved_window_id = zeroUuid();
    var has_target_selection_moved_column = false;
    var target_selection_moved_column_id = zeroUuid();
    var has_reuse_target_column = false;
    var reuse_target_column_id = zeroUuid();
    var create_target_visible_count: i64 = apply_request.request.max_visible_columns;
    var prune_target_empty_columns_if_no_windows = false;
    const max_edits = @min(plan_result.edit_count, abi.OMNI_NIRI_WORKSPACE_MAX_EDITS);
    for (0..max_edits) |idx| {
        const edit = plan_result.edits[idx];
        switch (edit.kind) {
            abi.OMNI_NIRI_WORKSPACE_EDIT_SET_SOURCE_SELECTION_WINDOW => {
                source_selection_window_id = preWindowId(
                    &pre_source_window_ids,
                    source_state.window_count,
                    edit.subject_index,
                ) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
                has_source_selection_window_id = true;
                source_selection_cleared = false;
            },
            abi.OMNI_NIRI_WORKSPACE_EDIT_SET_SOURCE_SELECTION_NONE => {
                has_source_selection_window_id = false;
                source_selection_cleared = true;
            },
            abi.OMNI_NIRI_WORKSPACE_EDIT_REUSE_TARGET_EMPTY_COLUMN => {
                reuse_target_column_id = preColumnId(
                    &pre_target_column_ids,
                    target_state.column_count,
                    edit.subject_index,
                ) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
                has_reuse_target_column = true;
                create_target_visible_count = edit.value_a;
            },
            abi.OMNI_NIRI_WORKSPACE_EDIT_CREATE_TARGET_COLUMN_APPEND => {
                create_target_visible_count = edit.value_a;
            },
            abi.OMNI_NIRI_WORKSPACE_EDIT_PRUNE_TARGET_EMPTY_COLUMNS_IF_NO_WINDOWS => {
                prune_target_empty_columns_if_no_windows = true;
            },
            abi.OMNI_NIRI_WORKSPACE_EDIT_REMOVE_SOURCE_COLUMN_IF_EMPTY => {
                if (remove_source_column_count >= abi.OMNI_NIRI_WORKSPACE_MAX_EDITS) return abi.OMNI_ERR_OUT_OF_RANGE;
                remove_source_column_ids[remove_source_column_count] = preColumnId(
                    &pre_source_column_ids,
                    source_state.column_count,
                    edit.subject_index,
                ) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
                remove_source_column_count += 1;
            },
            abi.OMNI_NIRI_WORKSPACE_EDIT_ENSURE_SOURCE_PLACEHOLDER_IF_NO_COLUMNS => {},
            abi.OMNI_NIRI_WORKSPACE_EDIT_SET_TARGET_SELECTION_MOVED_WINDOW => {
                target_selection_moved_window_id = preWindowId(
                    &pre_source_window_ids,
                    source_state.window_count,
                    edit.subject_index,
                ) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
                has_target_selection_moved_window = true;
                has_target_selection_moved_column = false;
            },
            abi.OMNI_NIRI_WORKSPACE_EDIT_SET_TARGET_SELECTION_MOVED_COLUMN_FIRST_WINDOW => {
                target_selection_moved_column_id = preColumnId(
                    &pre_source_column_ids,
                    source_state.column_count,
                    edit.subject_index,
                ) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
                has_target_selection_moved_column = true;
                has_target_selection_moved_window = false;
            },
            else => return abi.OMNI_ERR_INVALID_ARGS,
        }
    }
    if (prune_target_empty_columns_if_no_windows and target_state.window_count == 0) {
        var idx: usize = 0;
        while (idx < target_state.column_count) {
            if (target_state.columns[idx].window_count == 0) {
                _ = removeColumnAt(target_state, idx);
            } else {
                idx += 1;
            }
        }
    }
    var moved_window_id_opt: ?abi.OmniUuid128 = null;
    switch (apply_request.request.op) {
        abi.OMNI_NIRI_WORKSPACE_OP_MOVE_WINDOW_TO_WORKSPACE => {
            const moving_window_id = preWindowId(
                &pre_source_window_ids,
                source_state.window_count,
                apply_request.request.source_window_index,
            ) orelse return workspaceFail(abi.OMNI_ERR_OUT_OF_RANGE, "moving_window_id");
            const source_window_idx = findWindowIndexByIdCoherent(source_state, moving_window_id) orelse return workspaceFail(abi.OMNI_ERR_OUT_OF_RANGE, "source_window_idx");
            const source_column_idx = source_state.windows[source_window_idx].column_index;
            if (source_column_idx >= source_state.column_count) return workspaceFail(abi.OMNI_ERR_OUT_OF_RANGE, "source_column_idx");
            var target_column_id: abi.OmniUuid128 = undefined;
            if (has_reuse_target_column) {
                const target_column_idx = findColumnIndexByIdCoherent(target_state, reuse_target_column_id) orelse return workspaceFail(abi.OMNI_ERR_OUT_OF_RANGE, "reuse_target_column_idx");
                if (target_state.columns[target_column_idx].window_count != 0) return workspaceFail(abi.OMNI_ERR_INVALID_ARGS, "reuse_target_not_empty");
                const visible_count_i32 = visibleCountFromRaw(create_target_visible_count);
                if (visible_count_i32 < 0) return workspaceFail(visible_count_i32, "visible_count_i32_reuse");
                const visible_count: usize = @intCast(visible_count_i32);
                target_state.columns[target_column_idx].size_value = 1.0 / @as(f64, @floatFromInt(visible_count));
                target_state.columns[target_column_idx].width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION;
                target_column_id = reuse_target_column_id;
            } else {
                if (apply_request.has_target_created_column_id == 0) return workspaceFail(abi.OMNI_ERR_INVALID_ARGS, "missing_target_created_column_id");
                const unique_target_rc = ensureUniqueColumnId(target_state, apply_request.target_created_column_id);
                if (unique_target_rc != abi.OMNI_OK) return workspaceFail(unique_target_rc, "target_created_column_id_not_unique");
                const visible_count_i32 = visibleCountFromRaw(create_target_visible_count);
                if (visible_count_i32 < 0) return workspaceFail(visible_count_i32, "visible_count_i32");
                const visible_count: usize = @intCast(visible_count_i32);
                const add_column_rc = insertColumnAt(target_state, target_state.column_count, .{
                    .column_id = apply_request.target_created_column_id,
                    .window_start = 0,
                    .window_count = 0,
                    .active_tile_idx = 0,
                    .is_tabbed = 0,
                    .size_value = 1.0 / @as(f64, @floatFromInt(visible_count)),
                    .width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION,
                    .is_full_width = 0,
                    .has_saved_width = 0,
                    .saved_width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION,
                    .saved_width_value = 1.0,
                });
                if (add_column_rc != abi.OMNI_OK) return workspaceFail(add_column_rc, "add_target_column");
                target_column_id = apply_request.target_created_column_id;
            }
            const moved_window = removeWindowAt(source_state, source_window_idx);
            if (source_state.columns[source_column_idx].window_count == 0) return workspaceFail(abi.OMNI_ERR_OUT_OF_RANGE, "source_window_count_zero");
            source_state.columns[source_column_idx].window_count -= 1;
            const target_column_idx = findColumnIndexByIdCoherent(target_state, target_column_id) orelse return workspaceFail(abi.OMNI_ERR_OUT_OF_RANGE, "target_column_idx_after_add");
            const target_column = target_state.columns[target_column_idx];
            const target_insert_idx = columnWindowStart(target_state, target_column_idx) + target_column.window_count;
            const insert_window_rc = insertWindowAt(target_state, target_insert_idx, moved_window);
            if (insert_window_rc != abi.OMNI_OK) return workspaceFail(insert_window_rc, "insert_window_into_target");
            target_state.columns[target_column_idx].window_count += 1;
            moved_window_id_opt = moving_window_id;
        },
        abi.OMNI_NIRI_WORKSPACE_OP_MOVE_COLUMN_TO_WORKSPACE => {
            const moving_column_id = preColumnId(
                &pre_source_column_ids,
                source_state.column_count,
                apply_request.request.source_column_index,
            ) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const source_column_idx = findColumnIndexByIdCoherent(source_state, moving_column_id) orelse return abi.OMNI_ERR_OUT_OF_RANGE;
            const moving_column = source_state.columns[source_column_idx];
            var moved_windows: [abi.MAX_WINDOWS]abi.OmniNiriRuntimeWindowState = undefined;
            const remove_window_rc = removeWindowRange(
                source_state,
                moving_column.window_start,
                moving_column.window_count,
                &moved_windows,
            );
            if (remove_window_rc != abi.OMNI_OK) return remove_window_rc;
            _ = removeColumnAt(source_state, source_column_idx);
            const add_column_rc = insertColumnAt(target_state, target_state.column_count, moving_column);
            if (add_column_rc != abi.OMNI_OK) return add_column_rc;
            const append_windows_rc = appendWindowBatch(target_state, &moved_windows, moving_column.window_count);
            if (append_windows_rc != abi.OMNI_OK) return append_windows_rc;
            if (moving_column.window_count > 0) {
                moved_window_id_opt = moved_windows[0].window_id;
            }
        },
        else => return workspaceFail(abi.OMNI_ERR_INVALID_ARGS, "unknown_workspace_op"),
    }
    for (0..remove_source_column_count) |idx| {
        const remove_id = remove_source_column_ids[idx];
        const remove_idx_opt = findColumnIndexByIdCoherent(source_state, remove_id);
        if (remove_idx_opt) |remove_idx| {
            if (source_state.columns[remove_idx].window_count == 0) {
                _ = removeColumnAt(source_state, remove_idx);
            }
        }
    }
    if (apply_request.request.op == abi.OMNI_NIRI_WORKSPACE_OP_MOVE_WINDOW_TO_WORKSPACE and target_had_no_windows_before_move) {
        var idx: usize = 0;
        while (idx < target_state.column_count) {
            if (target_state.columns[idx].window_count == 0) {
                _ = removeColumnAt(target_state, idx);
            } else {
                idx += 1;
            }
        }
    }
    const should_insert_source_placeholder = source_state.column_count == 0 and apply_request.has_source_placeholder_column_id != 0;
    if (should_insert_source_placeholder) {
        const unique_placeholder_rc = ensureUniqueColumnId(source_state, apply_request.source_placeholder_column_id);
        if (unique_placeholder_rc != abi.OMNI_OK) return unique_placeholder_rc;
        const add_placeholder_rc = insertColumnAt(source_state, 0, .{
            .column_id = apply_request.source_placeholder_column_id,
            .window_start = 0,
            .window_count = 0,
            .active_tile_idx = 0,
            .is_tabbed = 0,
            .size_value = 1.0,
            .width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION,
            .is_full_width = 0,
            .has_saved_width = 0,
            .saved_width_kind = abi.OMNI_NIRI_SIZE_KIND_PROPORTION,
            .saved_width_value = 1.0,
        });
        if (add_placeholder_rc != abi.OMNI_OK) return add_placeholder_rc;
    }
    const source_refresh_rc = refreshRuntimeStateFast(source_state);
    if (source_refresh_rc != abi.OMNI_OK) return workspaceFail(source_refresh_rc, "source_final_refresh");
    const target_refresh_rc = refreshRuntimeStateFast(target_state);
    if (target_refresh_rc != abi.OMNI_OK) return workspaceFail(target_refresh_rc, "target_final_refresh");
    const source_validation_rc = validateRuntimeState(source_state);
    if (source_validation_rc != abi.OMNI_OK) return workspaceFail(source_validation_rc, "source_final_validation");
    const target_validation_rc = validateRuntimeState(target_state);
    if (target_validation_rc != abi.OMNI_OK) return workspaceFail(target_validation_rc, "target_final_validation");
    if (source_selection_cleared) {
        source_meta.has_source_selection_window_id = false;
    } else if (has_source_selection_window_id) {
        if (findWindowIndexByIdCoherent(source_state, source_selection_window_id) != null) {
            source_meta.has_source_selection_window_id = true;
            source_meta.source_selection_window_id = source_selection_window_id;
        }
    }
    if (has_target_selection_moved_window) {
        if (findWindowIndexByIdCoherent(target_state, target_selection_moved_window_id) != null) {
            target_meta.has_target_selection_window_id = true;
            target_meta.target_selection_window_id = target_selection_moved_window_id;
        }
    } else if (has_target_selection_moved_column) {
        if (findColumnIndexByIdCoherent(target_state, target_selection_moved_column_id)) |column_idx| {
            const column = target_state.columns[column_idx];
            if (column.window_count > 0) {
                target_meta.has_target_selection_window_id = true;
                target_meta.target_selection_window_id = target_state.windows[column.window_start].window_id;
            }
        }
    }
    if (moved_window_id_opt) |moved_window_id| {
        if (findWindowIndexByIdCoherent(target_state, moved_window_id) != null) {
            target_meta.has_moved_window_id = true;
            target_meta.moved_window_id = moved_window_id;
        }
    }
    commitRuntimeState(source_ctx, source_state);
    commitRuntimeState(target_ctx, target_state);
    out_result[0].applied = 1;
    out_result[0].changed_source_context = 1;
    out_result[0].changed_target_context = 1;
    out_result[0].structural_animation_active = 1;
    return abi.OMNI_OK;
}
pub fn omni_niri_ctx_export_delta_impl(
    context: [*c]const OmniNiriLayoutContext,
    out_export: [*c]abi.OmniNiriTxnDeltaExport,
) i32 {
    const ctx = asConstContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    if (out_export == null) return abi.OMNI_ERR_INVALID_ARGS;
    out_export[0] = .{
        .columns = if (ctx.last_delta_column_count > 0) @ptrCast(&ctx.last_delta_columns[0]) else null,
        .column_count = ctx.last_delta_column_count,
        .windows = if (ctx.last_delta_window_count > 0) @ptrCast(&ctx.last_delta_windows[0]) else null,
        .window_count = ctx.last_delta_window_count,
        .removed_column_ids = if (ctx.last_delta_removed_column_count > 0) @ptrCast(&ctx.last_delta_removed_column_ids[0]) else null,
        .removed_column_count = ctx.last_delta_removed_column_count,
        .removed_window_ids = if (ctx.last_delta_removed_window_count > 0) @ptrCast(&ctx.last_delta_removed_window_ids[0]) else null,
        .removed_window_count = ctx.last_delta_removed_window_count,
        .refresh_tabbed_visibility_count = ctx.last_delta_refresh_count,
        .refresh_tabbed_visibility_column_ids = ctx.last_delta_refresh_column_ids,
        .reset_all_column_cached_widths = ctx.last_delta_reset_all_column_cached_widths,
        .has_delegate_move_column = ctx.last_delta_has_delegate_move_column,
        .delegate_move_column_id = ctx.last_delta_delegate_move_column_id,
        .delegate_move_direction = ctx.last_delta_delegate_move_direction,
        .has_target_window_id = ctx.last_delta_has_target_window_id,
        .target_window_id = ctx.last_delta_target_window_id,
        .has_target_node_id = ctx.last_delta_has_target_node_id,
        .target_node_kind = ctx.last_delta_target_node_kind,
        .target_node_id = ctx.last_delta_target_node_id,
        .has_source_selection_window_id = ctx.last_delta_has_source_selection_window_id,
        .source_selection_window_id = ctx.last_delta_source_selection_window_id,
        .has_target_selection_window_id = ctx.last_delta_has_target_selection_window_id,
        .target_selection_window_id = ctx.last_delta_target_selection_window_id,
        .has_moved_window_id = ctx.last_delta_has_moved_window_id,
        .moved_window_id = ctx.last_delta_moved_window_id,
        .generation = ctx.last_delta_generation,
    };
    return abi.OMNI_OK;
}
pub fn omni_niri_ctx_start_workspace_switch_animation_impl(
    context: [*c]OmniNiriLayoutContext,
    sample_time: f64,
) i32 {
    const ctx = asMutableContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    startRuntimeAnimation(
        ctx,
        RUNTIME_ANIMATION_WORKSPACE_SWITCH,
        sample_time,
        NIRI_WORKSPACE_SWITCH_ANIMATION_DURATION,
    );
    return abi.OMNI_OK;
}
pub fn omni_niri_ctx_cancel_animation_impl(
    context: [*c]OmniNiriLayoutContext,
) i32 {
    const ctx = asMutableContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    clearRuntimeAnimationState(ctx);
    return abi.OMNI_OK;
}
pub fn omni_niri_ctx_animation_active_impl(
    context: [*c]OmniNiriLayoutContext,
    sample_time: f64,
    out_active: [*c]u8,
) i32 {
    const ctx = asMutableContext(context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    if (out_active == null) return abi.OMNI_ERR_INVALID_ARGS;
    out_active[0] = @intFromBool(syncRuntimeAnimationState(ctx, sample_time));
    return abi.OMNI_OK;
}
pub fn omni_niri_ctx_apply_txn_impl(
    source_context: [*c]OmniNiriLayoutContext,
    target_context: [*c]OmniNiriLayoutContext,
    request: [*c]const abi.OmniNiriTxnRequest,
    out_result: [*c]abi.OmniNiriTxnResult,
) i32 {
    const source_ctx = asMutableContext(source_context) orelse return abi.OMNI_ERR_INVALID_ARGS;
    if (request == null or out_result == null) return abi.OMNI_ERR_INVALID_ARGS;
    initTxnResult(out_result);
    out_result[0].kind = request[0].kind;
    const pre_source_state = runtimeStateFromContext(source_ctx);
    var source_state = pre_source_state;
    var source_delta_meta = initTxnDeltaMeta();
    switch (request[0].kind) {
        abi.OMNI_NIRI_TXN_NAVIGATION => {
            const rc = applyNavigationTxn(
                source_ctx,
                &source_state,
                request[0].navigation,
                out_result,
                &source_delta_meta,
            );
            out_result[0].error_code = rc;
            if (rc != abi.OMNI_OK) return rc;
            const delta_rc = storeTxnDeltaForContext(source_ctx, &pre_source_state, &source_delta_meta);
            if (delta_rc != abi.OMNI_OK) return delta_rc;
            out_result[0].delta_column_count = source_ctx.last_delta_column_count;
            out_result[0].delta_window_count = source_ctx.last_delta_window_count;
            out_result[0].removed_column_count = source_ctx.last_delta_removed_column_count;
            out_result[0].removed_window_count = source_ctx.last_delta_removed_window_count;
            return abi.OMNI_OK;
        },
        abi.OMNI_NIRI_TXN_MUTATION => {
            const rc = applyMutationTxn(
                source_ctx,
                &source_state,
                request[0].mutation,
                out_result,
                &source_delta_meta,
            );
            out_result[0].error_code = rc;
            if (rc != abi.OMNI_OK) return rc;
            const delta_rc = storeTxnDeltaForContext(source_ctx, &pre_source_state, &source_delta_meta);
            if (delta_rc != abi.OMNI_OK) return delta_rc;
            out_result[0].delta_column_count = source_ctx.last_delta_column_count;
            out_result[0].delta_window_count = source_ctx.last_delta_window_count;
            out_result[0].removed_column_count = source_ctx.last_delta_removed_column_count;
            out_result[0].removed_window_count = source_ctx.last_delta_removed_window_count;
            return abi.OMNI_OK;
        },
        abi.OMNI_NIRI_TXN_WORKSPACE => {
            const target_ctx = asMutableContext(target_context) orelse return abi.OMNI_ERR_INVALID_ARGS;
            const pre_target_state = runtimeStateFromContext(target_ctx);
            var target_state = pre_target_state;
            var source_meta = initTxnDeltaMeta();
            var target_meta = initTxnDeltaMeta();
            const rc = applyWorkspaceTxn(
                source_ctx,
                target_ctx,
                &source_state,
                &target_state,
                request[0].workspace,
                out_result,
                &source_meta,
                &target_meta,
            );
            out_result[0].error_code = rc;
            if (rc != abi.OMNI_OK) return rc;
            const source_delta_rc = storeTxnDeltaForContext(source_ctx, &pre_source_state, &source_meta);
            if (source_delta_rc != abi.OMNI_OK) return source_delta_rc;
            const target_delta_rc = storeTxnDeltaForContext(target_ctx, &pre_target_state, &target_meta);
            if (target_delta_rc != abi.OMNI_OK) return target_delta_rc;
            out_result[0].delta_column_count = source_ctx.last_delta_column_count + target_ctx.last_delta_column_count;
            out_result[0].delta_window_count = source_ctx.last_delta_window_count + target_ctx.last_delta_window_count;
            out_result[0].removed_column_count = source_ctx.last_delta_removed_column_count + target_ctx.last_delta_removed_column_count;
            out_result[0].removed_window_count = source_ctx.last_delta_removed_window_count + target_ctx.last_delta_removed_window_count;
            return abi.OMNI_OK;
        },
        else => {
            out_result[0].error_code = abi.OMNI_ERR_INVALID_ARGS;
            return abi.OMNI_ERR_INVALID_ARGS;
        },
    }
}

fn testUuid(seed: u8) abi.OmniUuid128 {
    return .{ .bytes = [_]u8{seed} ** 16 };
}

fn testRuntimeColumn(
    column_id: abi.OmniUuid128,
    window_start: usize,
    window_count: usize,
    size_value: f64,
    width_kind: u8,
    is_full_width: bool,
) abi.OmniNiriRuntimeColumnState {
    return .{
        .column_id = column_id,
        .window_start = window_start,
        .window_count = window_count,
        .active_tile_idx = 0,
        .is_tabbed = 0,
        .size_value = size_value,
        .width_kind = width_kind,
        .is_full_width = if (is_full_width) 1 else 0,
        .has_saved_width = 0,
        .saved_width_kind = width_kind,
        .saved_width_value = size_value,
    };
}

fn testRuntimeWindow(
    window_id: abi.OmniUuid128,
    column_id: abi.OmniUuid128,
    column_index: usize,
) abi.OmniNiriRuntimeWindowState {
    return .{
        .window_id = window_id,
        .column_id = column_id,
        .column_index = column_index,
        .size_value = 1.0,
        .height_kind = abi.OMNI_NIRI_HEIGHT_KIND_AUTO,
        .height_value = 1.0,
    };
}

fn setupViewportTestContext(
    ctx: *OmniNiriLayoutContext,
    columns: [3]abi.OmniNiriRuntimeColumnState,
) void {
    resetContext(ctx);
    ctx.runtime_column_count = columns.len;
    ctx.runtime_window_count = columns.len;
    for (columns, 0..) |column, idx| {
        ctx.runtime_columns[idx] = column;
        ctx.runtime_windows[idx] = testRuntimeWindow(
            testUuid(@intCast(40 + idx)),
            column.column_id,
            idx,
        );
    }
}

fn expectApproxEq(actual: f64, expected: f64) !void {
    try std.testing.expectApproxEqAbs(expected, actual, 0.000001);
}

test "deriveRuntimeViewportSpans handles horizontal mixed widths and full-width columns" {
    var ctx = std.mem.zeroes(OmniNiriLayoutContext);
    const column_ids = [_]abi.OmniUuid128{ testUuid(1), testUuid(2), testUuid(3) };
    setupViewportTestContext(
        &ctx,
        .{
            testRuntimeColumn(column_ids[0], 0, 1, 0.5, abi.OMNI_NIRI_SIZE_KIND_PROPORTION, false),
            testRuntimeColumn(column_ids[1], 1, 1, 220.0, abi.OMNI_NIRI_SIZE_KIND_FIXED, false),
            testRuntimeColumn(column_ids[2], 2, 1, 0.2, abi.OMNI_NIRI_SIZE_KIND_PROPORTION, true),
        },
    );

    var spans: [abi.MAX_WINDOWS]f64 = undefined;
    const rc = deriveRuntimeViewportSpans(&ctx, 10.0, 1000.0, &spans);
    try std.testing.expectEqual(abi.OMNI_OK, rc);
    try expectApproxEq(spans[0], 495.0);
    try expectApproxEq(spans[1], 220.0);
    try expectApproxEq(spans[2], 1000.0);

    ctx.viewport_state.active_column_index = 2;
    ctx.viewport_state.static_offset = -35.0;
    const view_start = runtimeViewportViewStart(&ctx, 0.0, &spans, ctx.runtime_column_count, 10.0);
    try expectApproxEq(view_start, 700.0);
}

test "deriveRuntimeViewportSpans handles vertical spans with gap math" {
    var ctx = std.mem.zeroes(OmniNiriLayoutContext);
    const column_ids = [_]abi.OmniUuid128{ testUuid(10), testUuid(11), testUuid(12) };
    setupViewportTestContext(
        &ctx,
        .{
            testRuntimeColumn(column_ids[0], 0, 1, 180.0, abi.OMNI_NIRI_SIZE_KIND_FIXED, false),
            testRuntimeColumn(column_ids[1], 1, 1, 0.25, abi.OMNI_NIRI_SIZE_KIND_PROPORTION, false),
            testRuntimeColumn(column_ids[2], 2, 1, 0.3, abi.OMNI_NIRI_SIZE_KIND_PROPORTION, true),
        },
    );

    var spans: [abi.MAX_WINDOWS]f64 = undefined;
    const rc = deriveRuntimeViewportSpans(&ctx, 8.0, 800.0, &spans);
    try std.testing.expectEqual(abi.OMNI_OK, rc);
    try expectApproxEq(spans[0], 180.0);
    try expectApproxEq(spans[1], 198.0);
    try expectApproxEq(spans[2], 800.0);
}

test "runtime viewport gesture update matches direct viewport kernel with derived spans" {
    var ctx = std.mem.zeroes(OmniNiriLayoutContext);
    const column_ids = [_]abi.OmniUuid128{ testUuid(21), testUuid(22), testUuid(23) };
    setupViewportTestContext(
        &ctx,
        .{
            testRuntimeColumn(column_ids[0], 0, 1, 0.35, abi.OMNI_NIRI_SIZE_KIND_PROPORTION, false),
            testRuntimeColumn(column_ids[1], 1, 1, 260.0, abi.OMNI_NIRI_SIZE_KIND_FIXED, false),
            testRuntimeColumn(column_ids[2], 2, 1, 0.25, abi.OMNI_NIRI_SIZE_KIND_PROPORTION, false),
        },
    );
    ctx.viewport_state.active_column_index = 1;
    ctx.viewport_state.static_offset = -24.0;

    const gap = 12.0;
    const viewport_span = 900.0;
    const delta_pixels = -110.0;
    const timestamp = 10.016;

    try std.testing.expectEqual(
        abi.OMNI_OK,
        omni_niri_ctx_viewport_begin_gesture_impl(@ptrCast(&ctx), 10.0, 1),
    );

    var ctx_update = std.mem.zeroes(abi.OmniViewportGestureUpdateResult);
    try std.testing.expectEqual(
        abi.OMNI_OK,
        omni_niri_ctx_viewport_update_gesture_impl(
            @ptrCast(&ctx),
            delta_pixels,
            timestamp,
            gap,
            viewport_span,
            &ctx_update,
        ),
    );

    var spans: [abi.MAX_WINDOWS]f64 = undefined;
    try std.testing.expectEqual(
        abi.OMNI_OK,
        deriveRuntimeViewportSpans(&ctx, gap, viewport_span, &spans),
    );
    const spans_ptr: [*c]const f64 = @ptrCast(&spans[0]);

    var direct_gesture = zeroViewportGestureState();
    try std.testing.expectEqual(
        abi.OMNI_OK,
        viewport.omni_viewport_gesture_begin_impl(-24.0, 1, &direct_gesture),
    );

    var direct_update = std.mem.zeroes(abi.OmniViewportGestureUpdateResult);
    const active_index = runtimeViewportActiveColumnIndex(&ctx, ctx.runtime_column_count);
    try std.testing.expectEqual(
        abi.OMNI_OK,
        viewport.omni_viewport_gesture_update_impl(
            &direct_gesture,
            spans_ptr,
            ctx.runtime_column_count,
            active_index,
            delta_pixels,
            timestamp,
            gap,
            viewport_span,
            0.0,
            &direct_update,
        ),
    );

    try expectApproxEq(ctx_update.current_view_offset, direct_update.current_view_offset);
    try expectApproxEq(ctx_update.selection_progress, direct_update.selection_progress);
    try std.testing.expectEqual(ctx_update.has_selection_steps, direct_update.has_selection_steps);
    try std.testing.expectEqual(ctx_update.selection_steps, direct_update.selection_steps);
    try expectApproxEq(ctx.viewport_state.gesture_state.current_view_offset, direct_update.current_view_offset);
    try expectApproxEq(ctx.viewport_state.selection_progress, direct_update.selection_progress);
}

test "runtime viewport gesture end matches direct viewport kernel with derived spans" {
    var ctx = std.mem.zeroes(OmniNiriLayoutContext);
    const column_ids = [_]abi.OmniUuid128{ testUuid(31), testUuid(32), testUuid(33) };
    setupViewportTestContext(
        &ctx,
        .{
            testRuntimeColumn(column_ids[0], 0, 1, 0.4, abi.OMNI_NIRI_SIZE_KIND_PROPORTION, false),
            testRuntimeColumn(column_ids[1], 1, 1, 300.0, abi.OMNI_NIRI_SIZE_KIND_FIXED, false),
            testRuntimeColumn(column_ids[2], 2, 1, 0.2, abi.OMNI_NIRI_SIZE_KIND_PROPORTION, false),
        },
    );
    ctx.viewport_state.active_column_index = 1;
    ctx.viewport_state.static_offset = -40.0;

    const gap = 10.0;
    const viewport_span = 1000.0;

    try std.testing.expectEqual(
        abi.OMNI_OK,
        omni_niri_ctx_viewport_begin_gesture_impl(@ptrCast(&ctx), 5.0, 1),
    );

    var ctx_update = std.mem.zeroes(abi.OmniViewportGestureUpdateResult);
    try std.testing.expectEqual(
        abi.OMNI_OK,
        omni_niri_ctx_viewport_update_gesture_impl(
            @ptrCast(&ctx),
            -140.0,
            5.016,
            gap,
            viewport_span,
            &ctx_update,
        ),
    );

    var spans: [abi.MAX_WINDOWS]f64 = undefined;
    try std.testing.expectEqual(
        abi.OMNI_OK,
        deriveRuntimeViewportSpans(&ctx, gap, viewport_span, &spans),
    );
    const spans_ptr: [*c]const f64 = @ptrCast(&spans[0]);

    var direct_gesture = zeroViewportGestureState();
    try std.testing.expectEqual(
        abi.OMNI_OK,
        viewport.omni_viewport_gesture_begin_impl(-40.0, 1, &direct_gesture),
    );
    var direct_update = std.mem.zeroes(abi.OmniViewportGestureUpdateResult);
    try std.testing.expectEqual(
        abi.OMNI_OK,
        viewport.omni_viewport_gesture_update_impl(
            &direct_gesture,
            spans_ptr,
            ctx.runtime_column_count,
            1,
            -140.0,
            5.016,
            gap,
            viewport_span,
            0.0,
            &direct_update,
        ),
    );

    var ctx_end = std.mem.zeroes(abi.OmniViewportGestureEndResult);
    try std.testing.expectEqual(
        abi.OMNI_OK,
        omni_niri_ctx_viewport_end_gesture_impl(
            @ptrCast(&ctx),
            gap,
            viewport_span,
            abi.OMNI_CENTER_ON_OVERFLOW,
            0,
            5.033,
            120.0,
            0,
            &ctx_end,
        ),
    );

    var direct_end = std.mem.zeroes(abi.OmniViewportGestureEndResult);
    try std.testing.expectEqual(
        abi.OMNI_OK,
        viewport.omni_viewport_gesture_end_impl(
            &direct_gesture,
            spans_ptr,
            ctx.runtime_column_count,
            1,
            gap,
            viewport_span,
            abi.OMNI_CENTER_ON_OVERFLOW,
            0,
            &direct_end,
        ),
    );

    try std.testing.expectEqual(ctx_end.resolved_column_index, direct_end.resolved_column_index);
    try expectApproxEq(ctx_end.spring_from, direct_end.spring_from);
    try expectApproxEq(ctx_end.spring_to, direct_end.spring_to);
    try expectApproxEq(ctx_end.initial_velocity, direct_end.initial_velocity);
    try std.testing.expectEqual(@as(i64, @intCast(direct_end.resolved_column_index)), ctx.viewport_state.active_column_index);
    try std.testing.expectEqual(@as(u8, 0), ctx.viewport_state.gesture_active);
    try std.testing.expectEqual(@as(u8, 1), ctx.viewport_state.spring_active);
}

test "runtime viewport transition matches direct viewport kernel with derived spans" {
    var ctx = std.mem.zeroes(OmniNiriLayoutContext);
    const column_ids = [_]abi.OmniUuid128{ testUuid(41), testUuid(42), testUuid(43) };
    setupViewportTestContext(
        &ctx,
        .{
            testRuntimeColumn(column_ids[0], 0, 1, 0.3, abi.OMNI_NIRI_SIZE_KIND_PROPORTION, false),
            testRuntimeColumn(column_ids[1], 1, 1, 260.0, abi.OMNI_NIRI_SIZE_KIND_FIXED, false),
            testRuntimeColumn(column_ids[2], 2, 1, 0.3, abi.OMNI_NIRI_SIZE_KIND_PROPORTION, false),
        },
    );

    ctx.viewport_state.active_column_index = 1;
    ctx.viewport_state.static_offset = -30.0;

    const gap = 14.0;
    const viewport_span = 960.0;
    const requested_index: usize = 2;

    var spans: [abi.MAX_WINDOWS]f64 = undefined;
    try std.testing.expectEqual(
        abi.OMNI_OK,
        deriveRuntimeViewportSpans(&ctx, gap, viewport_span, &spans),
    );
    const spans_ptr: [*c]const f64 = @ptrCast(&spans[0]);

    var direct = std.mem.zeroes(abi.OmniViewportTransitionResult);
    try std.testing.expectEqual(
        abi.OMNI_OK,
        viewport.omni_viewport_transition_to_column_impl(
            spans_ptr,
            ctx.runtime_column_count,
            1,
            requested_index,
            gap,
            viewport_span,
            -30.0,
            abi.OMNI_CENTER_NEVER,
            0,
            ctx.viewport_state.active_column_index,
            2.0,
            &direct,
        ),
    );

    var from_ctx = std.mem.zeroes(abi.OmniViewportTransitionResult);
    try std.testing.expectEqual(
        abi.OMNI_OK,
        omni_niri_ctx_viewport_transition_to_column_impl(
            @ptrCast(&ctx),
            requested_index,
            gap,
            viewport_span,
            abi.OMNI_CENTER_NEVER,
            0,
            0,
            2.0,
            12.0,
            60.0,
            0,
            &from_ctx,
        ),
    );

    try std.testing.expectEqual(from_ctx.resolved_column_index, direct.resolved_column_index);
    try expectApproxEq(from_ctx.offset_delta, direct.offset_delta);
    try expectApproxEq(from_ctx.adjusted_target_offset, direct.adjusted_target_offset);
    try expectApproxEq(from_ctx.target_offset, direct.target_offset);
    try expectApproxEq(from_ctx.snap_delta, direct.snap_delta);
    try std.testing.expectEqual(from_ctx.snap_to_target_immediately, direct.snap_to_target_immediately);

    if (from_ctx.snap_to_target_immediately != 0) {
        try expectApproxEq(ctx.viewport_state.static_offset, -30.0 + from_ctx.offset_delta + from_ctx.snap_delta);
    } else {
        try expectApproxEq(ctx.viewport_state.static_offset, from_ctx.target_offset);
    }
    try std.testing.expectEqual(@as(i64, @intCast(from_ctx.resolved_column_index)), ctx.viewport_state.active_column_index);
}
